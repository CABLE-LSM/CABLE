!==============================================================================
! This source code is part of the
! Australian Community Atmosphere Biosphere Land Exchange (CABLE) model.
! This work is licensed under the CABLE Academic User Licence Agreement
! (the "Licence").
! You may not use this file except in compliance with the Licence.
! A copy of the Licence and registration form can be obtained from
! http://www.accessimulator.org.au/cable
! You need to register and read the Licence agreement before use.
! Please contact cable_help@nf.nci.org.au for any questions on
! registration and the Licence.
!
! Unless required by applicable law or agreed to in writing,
! software distributed under the Licence is distributed on an "AS IS" BASIS,
! WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
! See the Licence for the specific language governing permissions and
! limitations under the Licence.
! ==============================================================================
!
! Purpose: All routines for calculating soil temperature and moisture
!          and snow calculations
!
! Contact: Eva.Kowalczyk@csiro.au
!
! History: v2.0 Tighter water budget
!          v2.0 Hydraulic redistribution subroutine (with namelist switch).
!               NB Currently hard-wired to veg types 2 and 7
!                  (usually evergreen broadleaf and c4 grass)
!          v2.0 ssoil variable renamed ssnow
!          Mark Decker - used ssnow as base for ssgw.  Could be part of same module
!          Aug 2017 - applied changes for cls and rev_corr packages
!                   - partner changes applied in soilsnow module
!
! ==============================================================================

MODULE cable_gw_hydro_module

  USE cable_def_types_mod, ONLY : soil_snow_type, soil_parameter_type,        &
       veg_parameter_type, canopy_type, met_type,        &
       balances_type, r_2, ms, mp

  USE cable_common_module, ONLY : gw_params,cable_user,&
       cable_runtime,&
       max_glacier_snowd,&
       psi_c   ! psi_c added by rk4417 - phase2

!  USE cbl_soil_snow_subrs_module, ONLY : trimb, snow_processes_soil_thermal ! replaced below by rk4417 - phase2

! line below commented out by rk4417 - phase2
!  USE cable_data_module, only: C=>PHYS ! all constants used in this module belong to PHYS


!distribute these per sbr   ! added by rk4417 - phase2
  USE cable_phys_constants_mod, ONLY : CTFRZ => TFRZ
  USE cable_phys_constants_mod, ONLY : CHL => HL 
  USE cable_phys_constants_mod, ONLY : CHLF => HLF
  USE cable_phys_constants_mod, ONLY : CHLS => HLS  ! not needed - rk4417 - phase2
  USE cable_phys_constants_mod, ONLY : Cdensity_liq => density_liq
  USE cable_phys_constants_mod, ONLY : Cdensity_ice => density_ice 
  USE cable_phys_constants_mod, ONLY : Ccgsnow => cgsnow 
  USE cable_phys_constants_mod, ONLY : Ccswat => cswat
  USE cable_phys_constants_mod, ONLY : Ccs_rho_wat => cs_rho_wat  ! not needed - rk4417 - phase2
  USE cable_phys_constants_mod, ONLY : Ccs_rho_ice => cs_rho_ice  ! not needed - rk4417 - phase2
  USE cable_phys_constants_mod, ONLY : Ccsice => csice   ! inserted by rk4417 - phase2
  USE cable_math_constants_mod, ONLY : CPI => PI
  
  IMPLICIT NONE

  PRIVATE

  !mrd561 GW params

!  REAL(r_2), SAVE :: smp_cor = 8.0 ! dependends on FEEDBACK - rk4417 - phase2
  
  REAL(r_2), PARAMETER :: sucmin       = -1.0e8      ! minimum soil pressure head [mm]
  REAL(r_2), PARAMETER :: volwatmin    = 1e-4        !min soil water [mm]
  REAL(r_2), PARAMETER :: wtd_uncert   = 0.1         ! uncertaintiy in wtd calcultations [mm]
  REAL(r_2), PARAMETER :: wtd_max      = 1000000.0   ! maximum wtd [mm]
  REAL(r_2), PARAMETER :: wtd_min      = 10.0        ! minimum wtd [mm]
  REAL(r_2), PARAMETER :: close_to_one = 0.9999
  REAL(r_2), PARAMETER :: Sy_deep = 0.1
  REAL(r_2), PARAMETER :: dz_deep = 50000.0

  REAL(r_2), SAVE      :: den_rat=0.921  

  INTEGER, PARAMETER :: wtd_iter_max = 20 ! maximum number of iterations to find the water table depth

   REAL(r_2), PARAMETER :: m2mm = 1000.0   
   REAL(r_2), PARAMETER :: mm2m = 0.001
   REAL(r_2), PARAMETER :: zero=0.0

  ! This module contains the following subroutines that
  !are called from other modules

  PUBLIC :: soil_snow_gw, calc_srf_wet_fraction, sli_hydrology,&
       pore_space_relative_humidity, set_unsed_gw_vars, den_rat
       !,set_den_rat  ! MMY@Nov2022 comment out set_den_rat

CONTAINS

  ! -----------------------------------------------------------------------------

    SUBROUTINE GWsoilfreeze(dels, soil, ssnow)

   !*## Purpose
   ! Snow freezes and melts. 
   ! this is only included because gw_model uses parameters XXX_vec
   ! these are r_2.  this breaks bitwise compatibility with trunk
   ! if acceptable this routine does the same thing but with r_2 soil params
   ! if max_ice_frac always set to frozen_limit and tgg_tmp is always CTFRZ
   !

    REAL, INTENT(IN)                    :: dels ! integration time step (s)
    TYPE(soil_snow_type), INTENT(INOUT)      :: ssnow
    TYPE(soil_parameter_type), INTENT(INOUT)    :: soil

    REAL     , DIMENSION(mp,ms)        :: tgg_tmp !calc wb ice at approx tgg 
    REAL(r_2), DIMENSION(mp,ms)        :: wbice_delta,avail_por,delta_ice_vol
    REAL(r_2), DIMENSION(mp)           :: ice_mass,liq_mass,tot_mass

    INTEGER :: i,j,k
    REAL(r_2) :: Aconst,Dconst
    REAL, DIMENSION(mp,ms) :: gammzz_snow
    REAL(r_2),DIMENSION(mp,ms) :: xx,max_ice_frac,den_css  !Decker and Zeng 2009 ! MMY iceF is useless 

   max_ice_frac(:,:) = 0.0 
   delta_ice_vol(:,:) = 0.0
   tgg_tmp(:,:) = ssnow%tgg(:,:)
   gammzz_snow(:,:) = 0._r_2

   k=1
   do i=1,mp
      if (ssnow%isflag(i) .eq. 0 .and. soil%isoilm(i) .ne. 9) then
           gammzz_snow(i,k) = real(Ccgsnow,r_2) * real(ssnow%snowd(i),r_2)
      end if
   end do

   do k=1,ms
   do i=1,mp

      if ( (ssnow%tgg(i,k) .lt. CTFRZ) .and. &
          (ssnow%tgg(i,k) .lt. ssnow%otgg(i,k)) ) then ! MMY freezing

            ssnow%otgg(i,k) = min(ssnow%otgg(i,k),CTFRZ)

            tgg_tmp(i,k) = ssnow%tgg(i,k)

            Aconst = -2.0*( (0.2+ssnow%wb(i,k)/soil%ssat_vec(i,k))**4.0 )
            Dconst = exp(1.  - min(1.0,0.2+ssnow%wb(i,k)/soil%ssat_vec(i,k)))

            max_ice_frac(i,k) = (1._r_2 - exp(2._r_2*(min(1.,ssnow%wb(i,k)/soil%ssat_vec(i,k))**4.0) *&
                          real(tgg_tmp(i,k)-CTFRZ,r_2)))/exp(1._r_2- min(1.0,ssnow%wb(i,k)/soil%ssat_vec(i,k)))

            if (soil%isoilm(i) .eq. 9) max_ice_frac(i,k) = 0.85_r_2

            ! MMY maximum ice can get under current wb
            max_ice_frac(i,k) = min(0.9_r_2,max_ice_frac(i,k))

            !delta_ice_vol(i,k) = max(0._r_2, ssnow%wb(i,k)*max_ice_frac(i,k) - ssnow%wbice(i,k)) ! MMY
            ! MMY ssnow%wb(i,k)*max_ice_frac(i,k) here should be ice volume, thus divided by den_rat
            delta_ice_vol(i,k) = max(0._r_2, ssnow%wb(i,k)*max_ice_frac(i,k)/den_rat - ssnow%wbice(i,k)) ! MMY

            ! MMY wbliq can be lower than watr due to condensation, but to avoid large negative water potential
            !     set watr as wbliq lower boundary
            !check amount of water we have
            delta_ice_vol(i,k) = min((ssnow%wbliq(i,k)-soil%watr(i,k))/den_rat, &
                                 max(0._r_2, delta_ice_vol(i,k) ) )

            delta_ice_vol(i,k) = min(delta_ice_vol(i,k),max(0._r_2,&
                                   real(ssnow%otgg(i,k)-ssnow%tgg(i,k),r_2)*ssnow%gammzz(i,k)/&
                                     (soil%zse_vec(i,k)*real(CHLF*Cdensity_ice,r_2)) ) )

      elseif ((ssnow%tgg(i,k) .gt. CTFRZ) .and. &
              (ssnow%tgg(i,k) .gt. ssnow%otgg(i,k)) .and. ssnow%wbice(i,k) .gt.  0.0) then ! MMY melting

              ssnow%otgg(i,k) = CTFRZ

             delta_ice_vol(i,k) = ssnow%wbice(i,k)

             delta_ice_vol(i,k) = min(delta_ice_vol(i,k), max(0._r_2,&
                                  real(ssnow%tgg(i,k)-ssnow%otgg(i,k),r_2) * ssnow%gammzz(i,k) /&
                                     (soil%zse_vec(i,k)*real(CHLF*Cdensity_ice,r_2)) ) )

      endif
   end do
   end do

   DO k = 1, ms
      DO i=1,mp

          if ( (ssnow%tgg(i,k) .lt. CTFRZ) .and. &
               (delta_ice_vol(i,k) .gt. 0.0)  ) then

            ssnow%wbice(i,k) = ssnow%wbice(i,k) + delta_ice_vol(i,k)
            ssnow%wbliq(i,k) = ssnow%wbliq(i,k) - delta_ice_vol(i,k)*den_rat

            ssnow%gammzz(i,k) = max(soil%heat_cap_lower_limit(i,k),  &
                              (1.0-soil%ssat_vec(i,k))*soil%css_vec(i,k)*soil%rhosoil_vec(i,k) &
                            + ssnow%wbliq(i,k) * REAL(Ccswat*Cdensity_liq,r_2)   &
                            + ssnow%wbice(i,k) * REAL(Ccsice*Cdensity_ice,r_2)&
                            )*soil%zse_vec(i,k)  + gammzz_snow(i,k)

            ! MMY freezing releases heat
            ssnow%tgg(i,k) = ssnow%tgg(i,k) + real( delta_ice_vol(i,k)*soil%zse_vec(i,k)  *&
                                       real(CHLF*Cdensity_ice,r_2) / ssnow%gammzz(i,k) )


            ssnow%wb(i,k) = ssnow%wbliq(i,k) + den_rat*ssnow%wbice(i,k)

            ssnow%wmliq(i,k) = ssnow%wbliq(i,k)*soil%zse_vec(i,k)*Cdensity_liq
            ssnow%wmice(i,k) = ssnow%wbice(i,k)*soil%zse_vec(i,k)*Cdensity_ice
            ssnow%wmtot(i,k) = ssnow%wb(i,k)   *soil%zse_vec(i,k)*Cdensity_liq

          elseif ((ssnow%tgg(i,k) .gt. CTFRZ) .and. &
              delta_ice_vol(i,k) .gt. 0.0 ) then ! MMY
            ! ssnow%wbice(i,k) .gt. 0.0 ) then ! MMY It's not right.
            ! MMY When previous tgg > current tgg > 0, wbice won't melt

            ssnow%wbice(i,k) = ssnow%wbice(i,k)  -  delta_ice_vol(i,k)
            ssnow%wbliq(i,k) = ssnow%wbliq(i,k)  +  delta_ice_vol(i,k)*den_rat

            ssnow%gammzz(i,k) = max(soil%heat_cap_lower_limit(i,k),  &
                              (1.0-soil%ssat_vec(i,k))*soil%css_vec(i,k)*soil%rhosoil_vec(i,k) &
                            + ssnow%wbliq(i,k) * REAL(Ccswat*Cdensity_liq,r_2)   &
                            + ssnow%wbice(i,k) * REAL(Ccsice*Cdensity_ice,r_2)&
                            )*soil%zse_vec(i,k)  + gammzz_snow(i,k)

            ! MMY melting absorbs heat
            ssnow%tgg(i,k) = ssnow%tgg(i,k) - real( delta_ice_vol(i,k)*soil%zse_vec(i,k)  *&
                                          real(CHLF*Cdensity_ice,r_2) / ssnow%gammzz(i,k) )

            ssnow%wb(i,k) = ssnow%wbliq(i,k) + den_rat*ssnow%wbice(i,k)

            ssnow%wmliq(i,k) = ssnow%wbliq(i,k)*soil%zse_vec(i,k)*Cdensity_liq
            ssnow%wmice(i,k) = ssnow%wbice(i,k)*soil%zse_vec(i,k)*Cdensity_ice
            ssnow%wmtot(i,k) = ssnow%wb(i,k)   *soil%zse_vec(i,k)*Cdensity_liq
          END IF


      END DO
   END DO

  END SUBROUTINE GWsoilfreeze
  ! -----------------------------------------------------------------------------
  !
  !! -----------------------------------------------------------------------------
  !
  SUBROUTINE remove_transGW(dels, soil, ssnow, canopy, veg)

   !*## Purpose
   !
    
    !NOTE: this is only included because gw_model uses parameters XXX_vec
    !these are r_2.  this breaks bitwise compatibility with trunk
    !if acceptable this routine does the same thing but with r_2 soil params

    ! Removes transpiration water from soil.
    REAL, INTENT(IN)                    :: dels ! integration time step (s)
    TYPE(canopy_type), INTENT(INOUT)         :: canopy
    TYPE(soil_snow_type), INTENT(INOUT)      :: ssnow
    TYPE(soil_parameter_type), INTENT(INOUT)    :: soil
    TYPE(veg_parameter_type), INTENT(INOUT)  :: veg
    REAL(r_2), DIMENSION(mp,0:ms+1) :: diff
    REAL(r_2), DIMENSION(mp)      :: xx,xxd
    REAL(r_2), DIMENSION(mp,ms) :: zse_mp_mm
    INTEGER :: k,i

    DO k=1,ms
       DO i=1,mp
          zse_mp_mm(i,k)  = REAL(soil%zse_vec(i,k)*Cdensity_liq,r_2)
       END DO
    END DO

    IF (cable_user%FWSOIL_switch.NE.'Haverd2013') THEN

       xx(:) = 0._r_2
       xxd(:) = 0._r_2
       diff(:,:) = 0._r_2

       DO k = 1,ms

          DO i=1,mp

             IF (canopy%fevc(i) .GT. 0._r_2) THEN

                xx(i) = canopy%fevc(i) * dels / CHL * veg%froot(i,k) + diff(i,k-1)
                diff(i,k) = MAX(0._r_2,ssnow%wbliq(i,k)-soil%swilt_vec(i,k)) &
                     * zse_mp_mm(i,k)
                xxd(i) = xx(i) - diff(i,k)

                IF (xxd(i) .GT. 0._r_2) THEN
                   ssnow%wbliq(i,k) = ssnow%wbliq(i,k) - diff(i,k)/zse_mp_mm(i,k)
                   diff(i,k) = xxd(i)
                ELSE
                   ssnow%wbliq(i,k) = ssnow%wbliq(i,k) - xx(i)/zse_mp_mm(i,k)
                   diff(i,k) = 0._r_2
                END IF


             END IF  !fvec > 0

          END DO  !mp
       END DO     !ms

    ELSE

       WHERE (canopy%fevc .LT. 0.0_r_2)
          canopy%fevw = canopy%fevw+canopy%fevc
          canopy%fevc = 0.0_r_2
       END WHERE
       DO k = 1,ms
          ssnow%wbliq(:,k) = ssnow%wbliq(:,k) - ssnow%evapfbl(:,k)/(soil%zse_vec(:,k)*m2mm)
       ENDDO

    ENDIF

    DO k=1,ms
       DO i=1,mp
          ssnow%wmliq(i,k) = ssnow%wbliq(i,k)*zse_mp_mm(i,k)!mass
          ssnow%wmtot(i,k) = ssnow%wmliq(i,k) + ssnow%wmice(i,k)  !mass
          ssnow%wb(i,k)    = ssnow%wbliq(i,k) + den_rat * ssnow%wbice(i,k)  !volume ! MMY
       END DO
    END DO


  END SUBROUTINE remove_transGW


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!MD GW code from here on!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !----------------------------------------------------------------------

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !-------------------------------------------------------------------------
  SUBROUTINE ovrlndflx (dels, ssnow, soil,veg, canopy,sli_call )
    !* Calculate surface runoff
    
    USE cable_common_module, ONLY : gw_params,cable_user

    USE grid_constants_mod_cbl, ONLY : lakes_cable

    IMPLICIT NONE
    REAL, INTENT(IN)                         :: dels ! integration time step (s)
    TYPE(soil_snow_type), INTENT(INOUT)      :: ssnow  ! soil+snow variables
    TYPE(soil_parameter_type), INTENT(INOUT)    :: soil ! soil parameters
    TYPE(veg_parameter_type) , INTENT(INOUT)    :: veg  ! veg parameters
    TYPE (canopy_type), INTENT(INOUT)           :: canopy
    LOGICAL, INTENT(IN)                      :: sli_call
    INTEGER                                  :: nglacier ! 0 original, 1 off, 2 new Eva
    INTEGER                                  :: k, i, j
    REAL, DIMENSION(mp)                :: rnof5
    REAL, DIMENSION(mp)                :: sgamm
    REAL, DIMENSION(mp)                :: smasstot
    REAL, DIMENSION(mp,0:3)            :: smelt1                   !snow melt
    REAL(r_2), DIMENSION(mp)           :: icef,efpor               !tmp vars, fraction of ice in gridcell
    REAL(r_2)                          :: tmpa,tmpb,qinmax         !tmp vars, maximum infiltration [mm/s]
    REAL(r_2), DIMENSION(mp)           :: satfrac_liqice,S       !saturated fraction of cell, wtd in m
    REAL(r_2)                          :: liqmass,icemass,totmass  !liquid mass,ice mass, total mass [mm]
    REAL(r_2)                          :: fice
    REAL(r_2)                          :: dzmm,slopeSTDmm

    !For now assume there is no puddle
    dzmm = 1000._r_2 * soil%zse(1)

    IF (sli_call) THEN
       DO i=1,mp
          IF (canopy%through(i) .GE. canopy%through_sn(i)) THEN
             ssnow%fwtop(i)  = MAX((canopy%through(i)-canopy%through_sn(i))/dels , 0.)             ! liq precip rate (m s-1)
          ELSE
             ssnow%fwtop(i) = MAX(canopy%through(i), 0.)
          END IF
       END DO
    END IF
    !amount of ice in surface layer
    DO i = 1,mp
       efpor(i) = max(0.01_r_2, soil%ssat_vec(i,1) - den_rat*ssnow%wbice(i,1)) 
       icemass  = ssnow%wmice(i,1)
       liqmass  = ssnow%wmliq(i,1)
       
       totmass  = MAX(liqmass+icemass,REAL(1e-2,r_2))
       icef(i)     = max(0._r_2,min(1._r_2,icemass / totmass))
    END DO

    !sat fraction assuming topo controlled subgrid soil moisture distribution
    !called from cable_canopy for srf wet fraction alrady

    call saturated_fraction(ssnow,soil,veg) ! give ssnow%satfrac value before it is used
    
    !srf frozen fraction.  should be based on topography
    DO i = 1,mp
       fice = (EXP(-gw_params%IceAlpha*(1._r_2-icef(i)))-EXP(-gw_params%IceAlpha))/(1._r_2-EXP(-gw_params%IceAlpha))
       fice  = MIN(MAX(fice,0._r_2),1._r_2)
       satfrac_liqice(i)   = MAX(0.,MIN(0.95,fice + (1._r_2-fice)*ssnow%satfrac(i) ) )
    END DO

    DO i=1,mp
       tmpa = ssnow%wbliq(i,1) / efpor(i)
       tmpb = MAX( (tmpa-satfrac_liqice(i))/MAX(0.01_r_2,(1._r_2-satfrac_liqice(i))), 0._r_2)
       tmpa = -2._r_2*soil%bch_vec(i,1)*soil%sucs_vec(i,1)/soil%zse_vec(i,1)/1000._r_2
       qinmax = (1._r_2 + tmpa*(tmpb-1._r_2))*soil%hyds_vec(i,1)*EXP(-gw_params%hkrz*(0.5*dzmm/1000.0_r_2-gw_params%zdepth))
       ! qinmax = (1._r_2 + tmpa*(tmpb-1._r_2))*soil%hyds_vec(i,1) ! MMY@23Apr2023, keep this commented line and the line above
                                                                   !              *EXP(-gw_params%hkrz*(0.5*dzmm/1000.0_r_2-gw_params%zdepth))
                                                                   !              should be in the eq if considering hk changes with soil depth
                                                                   !  However, *EXP(-gw_params%hkrz*(0.5*dzmm/1000.0_r_2-gw_params%zdepth)) may only
                                                                   !               make sense when soil properties are uniform in NO-GW codes.
                                                                   !  In CABLE-GW soil properties vary with depth, EXP(-gw_params%hkrz*(0.5*dzmm/
                                                                   !              1000.0_r_2-gw_params%zdepth)) complicates things. I think it should be
                                                                   !              removed everywhere.                                        
       ssnow%rnof1(i) = satfrac_liqice(i) * ssnow%fwtop(i) + &
            (1._r_2-satfrac_liqice(i))*MAX((ssnow%fwtop(i)-qinmax) , 0._r_2)

       ssnow%fwtop(i) = ssnow%fwtop(i) - ssnow%rnof1(i)

    END DO  !mp

    !add back to the lakes to keep saturated instead of drying
    DO i=1,mp
       IF (veg%iveg(i) .EQ. lakes_cable) THEN
          ssnow%fwtop(i) = ssnow%fwtop(i) + ssnow%rnof1(i)
          ssnow%rnof1(i) = 0._r_2
       END IF
    END DO

    !---  glacier formation
    rnof5= 0.

!$    IF (sli_call .OR. cable_runtime%UM) THEN
! added by rk4417 to conform with MMY modifications
    IF (sli_call .OR. cable_runtime%UM .OR. cable_user%gw_model) THEN   ! FEEDBACK (MMY asks: why cable_user%gw_model=True doesn't need to consider snow melting) --rk4417
       nglacier = 0
    ELSE
       nglacier = 2
    END IF

    IF (nglacier == 2) THEN
       smelt1=0.
       WHERE( ssnow%snowd > max_glacier_snowd )

          rnof5 = MIN( 0.1, ssnow%snowd - max_glacier_snowd )

          !---- change local tg to account for energy - clearly not best method
          WHERE( ssnow%isflag == 0 )
             smasstot = 0.0
             ssnow%tgg(:,1) = ssnow%tgg(:,1) - rnof5 * CHLF                    &
                  / REAL( ssnow%gammzz(:,1) )
             ssnow%snowd = ssnow%snowd - rnof5
          ELSEWHERE
             smasstot = ssnow%smass(:,1) + ssnow%smass(:,2) + ssnow%smass(:,3)
          END WHERE

       END WHERE

       DO k = 1, 3

          WHERE( ssnow%snowd > max_glacier_snowd  .AND.  ssnow%isflag > 0 )
             sgamm = ssnow%ssdn(:,k) * Ccgsnow * ssnow%sdepth(:,k)
             smelt1(:,k) = MIN( rnof5 * ssnow%smass(:,k) / smasstot,            &
                  0.2 * ssnow%smass(:,k) )
             ssnow%smass(:,k) = ssnow%smass(:,k) - smelt1(:,k)
             ssnow%snowd = ssnow%snowd - smelt1(:,k)
          END WHERE

       END DO

       WHERE( ssnow%isflag > 0 ) rnof5 = smelt1(:,1) + smelt1(:,2) + smelt1(:,3)

       ssnow%rnof1 = ssnow%rnof1 + rnof5/dels   !include this runoff in suface runoff term

    END IF

  END SUBROUTINE ovrlndflx

  !----------------------------------------------------------------------

  SUBROUTINE iterative_wtd (ssnow, soil, veg, include_aquifer)
  !
  !*## Purpose
  ! Iteratively calcs the water table depth by equating the mass of water in the
  ! soil column to the mass of a hydrostatic column inegrated from the surface to the
  ! water table depth
  ! ChatGPT: "The water table depth in soil can be calculated iteratively by equating the mass of water in the soil
  !           column to the mass of a hydrostatic column integrated from the surface to the water table depth. This
  !           method is based on the assumption that the water in the soil column is in hydrostatic equilibrium,
  !           which means that the pressure of the water at any point in the column is equal to the weight of the water
  !           above that point.
  !           The iterative process involves starting with an initial estimate of the water table depth and then
  !           calculating the mass of water in the soil column using that depth. This mass is then compared to the
  !           mass of the hydrostatic column integrated from the surface to the estimated water table depth. If the
  !           two masses are not equal, the estimated water table depth is adjusted and the process is repeated until
  !           convergence is achieved.
  !           The calculation of the mass of the hydrostatic column requires knowledge of the soil properties, such as
  !           porosity and permeability, as well as the water content of the soil. These parameters can be obtained through
  !           laboratory testing or estimated from field observations.
  !           Once the water table depth is determined, it can be used to assess the groundwater availability and potential
  !           impacts on nearby structures or ecosystems."
  ! Method:
  !          I cannot find where this subroutine come from. It is not from CLM5 or CLM4.5. It also looks not from SIMGM model in Niu et al 2007
    
    IMPLICIT NONE
    TYPE (soil_snow_type), INTENT(INOUT)      :: ssnow ! soil and snow variables
    TYPE (soil_parameter_type), INTENT(INOUT)    :: soil  ! soil parameters
    TYPE (veg_parameter_type), INTENT(INOUT)     :: veg
    LOGICAL, INTENT(IN)                       :: include_aquifer  !use GWwb or only wb to find wtd?

    REAL(r_2), DIMENSION(mp)      :: def,defc,total_depth_column
    REAL(r_2)                     :: deffunc,tempa,tempb,derv,calc,tmpc
    REAL(r_2), DIMENSION(mp)      :: lam,Nsucs_vec  !inverse of C&H B,Nsucs_vec
    INTEGER :: k,i,jlp

    lam(:)        = 1._r_2/soil%bch_vec(:,ms) !1 over C&H B      ! replaced block above as per MMY -- rk4417
    Nsucs_vec(:)  = abs(soil%sucs_vec(:,ms)) !psi_saturated mm

    ! MMY@23Apr2023, add aquifer thickness to the total depth and calcuate aquifer water content
    IF (include_aquifer) THEN  !do we include the aquifer in the calculation of wtd?

       DO i=1,mp
          total_depth_column(i) = soil%GWdz(i)*m2mm 
          def(i) = max(0._r_2,soil%GWssat_vec(i)-ssnow%GWwb(i))*soil%GWdz(i)*m2mm
       END DO
       
    ELSE                  
       def(:) = 0._r_2
       total_depth_column(:) = 0._r_2
    END IF

    !total depth of soil column
    do k=1,ms
       do i=1,mp
          total_depth_column(i) = total_depth_column(i) + soil%zse_vec(i,k)*m2mm
       end do
    end do
    
    !comute the total mass away from full saturation
    DO k=1,ms
       DO i=1,mp

          def(i) = def(i) +                                                           &
               max(0._r_2,(soil%ssat_vec(i,k)-(ssnow%wbliq(i,k)+den_rat*ssnow%wbice(i,k)))*soil%zse_vec(i,k)*m2mm)
       END DO  !mp
    END DO  !ms

    !find the deficit if the water table is at the bottom of the soil column
    DO i=1,mp
       defc(i) = (soil%ssat_vec(i,ms))*(total_depth_column(i)+Nsucs_vec(i)/(1._r_2-lam(i))*            &
            (1._r_2-((Nsucs_vec(i)+total_depth_column(i))/Nsucs_vec(i))**(1._r_2-lam(i))))
       defc(i) = MAX(0.1_r_2,defc(i))
    END DO
    
    !initial guess at wtd                                      ! taken out of do loop as per MMY -- rk4417
    ssnow%wtd(:) = total_depth_column(:)*def(:)/defc(:)

    !use newtons method to solve for wtd, note this assumes homogenous column but
    !that is ok
    DO i=1,mp
       IF ((soil%isoilm(i) .NE. 9) .AND. (veg%iveg(i) .NE. 16)) THEN

          IF (defc(i) > def(i)) THEN                 !iterate tfor wtd  ! MMY@23Apr2023 if soil deficit lower than total soil deficit

             jlp=0

             mainloop: DO

                tempa   = 1.0_r_2
                tempb   = (1._r_2+ssnow%wtd(i)/Nsucs_vec(i))**(-lam(i))
                derv    = (soil%ssat_vec(i,ms))*(tempa-tempb) + &
                     soil%ssat_vec(i,ms)

                IF (ABS(derv) .LT. REAL(1e-8,r_2)) derv = SIGN(REAL(1e-8,r_2),derv)

                tempa   = 1.0_r_2
                tempb   = (1._r_2+ssnow%wtd(i)/Nsucs_vec(i))**(1._r_2-lam(i))
                deffunc = (soil%ssat_vec(i,ms))*(ssnow%wtd(i) +&
                     Nsucs_vec(i)/(1-lam(i))* &
                     (tempa-tempb)) - def(i)
                calc    = ssnow%wtd(i) - deffunc/derv

                IF ((ABS(calc-ssnow%wtd(i))) .LE. wtd_uncert) THEN

                   ssnow%wtd(i) = calc
                   EXIT mainloop

                ELSEIF (jlp .GE. wtd_iter_max) THEN

                   EXIT mainloop

                ELSE

                   jlp=jlp+1
                   ssnow%wtd(i) = calc

                END IF

             END DO mainloop  !defc .gt. def

          ELSEIF (defc(i) .LT. def(i)) THEN    ! MMY@23Apr2023 if soil deficit larger than total soil deficit

             jlp=0

             mainloop2: DO

                tmpc     = Nsucs_vec(i)+ssnow%wtd(i)-total_depth_column(i)
                tempa    = (abs(tmpc/Nsucs_vec(i)))**(-lam(i))
                tempb    = (1._r_2+ssnow%wtd(i)/Nsucs_vec(i))**(-lam(i))
                derv     = (soil%ssat_vec(i,ms))*(tempa-tempb)
                IF (ABS(derv) .LT. REAL(1e-8,r_2)) derv = SIGN(REAL(1e-8,r_2),derv)
                tempa    = (abs((Nsucs_vec(i)+ssnow%wtd(i)-total_depth_column(i))/Nsucs_vec(i)))**(1._r_2-lam(i))
                tempb    = (1._r_2+ssnow%wtd(i)/Nsucs_vec(i))**(1._r_2-lam(i))
                deffunc  = (soil%ssat_vec(i,ms))*(total_depth_column(i) +&
                     Nsucs_vec(i)/(1._r_2-lam(i))*(tempa-tempb))-def(i)
                calc     = ssnow%wtd(i) - deffunc/derv

                IF ((ABS(calc-ssnow%wtd(i))) .LE. wtd_uncert) THEN

                   ssnow%wtd(i) = calc
                   EXIT mainloop2

                ELSEIF (jlp==wtd_iter_max) THEN

                   EXIT mainloop2

                ELSE

                   jlp=jlp+1
                   ssnow%wtd(i) = calc

                END IF

             END DO mainloop2  !defc .lt. def

          ELSE  !water table depth is exactly on bottom boundary

             ssnow%wtd(i) = total_depth_column(i)

          ENDIF

       ENDIF  !check veg and soils

    END DO   !mp loop

    !limit wtd to be within a psecified range
    DO i=1,mp
       IF (veg%iveg(i) .GE. 16) ssnow%wtd(i) = wtd_min
       ssnow%wtd(i) = MIN(wtd_max,MAX(wtd_min,ssnow%wtd(i) ) )
    END DO


  END SUBROUTINE iterative_wtd


  SUBROUTINE smoistgw (dels,ktau,ssnow,soil,veg,canopy)
  
   !* Solve the modified Richards equation 
   !  [Zeng and Decker 2009](https://doi.org/10.1175/2008JHM1011.1) 
   !  to find vertical movement of soil water. The modified Richards equation considers
   !  soil moisture distribution at hydrostatic equilibrium to avoid the truncation
   !  errors due to finite grid spacing used in the partial differential equation.
   !  Bottom boundary condition is determined using a single-layer groundwater module.
  
  USE cable_common_module
  USE trimb_mod, ONLY : trimb       ! inserted by rk4417 - phase2
  
    IMPLICIT NONE

    REAL, INTENT(IN)                          :: dels  ! time step size (s)
    INTEGER, INTENT(IN)                       :: ktau  ! integration step number
    TYPE (soil_snow_type), INTENT(INOUT)      :: ssnow ! soil and snow variables
    TYPE (soil_parameter_type), INTENT(INOUT)    :: soil  ! soil parameters
    TYPE (veg_parameter_type), INTENT(INOUT)     :: veg
    TYPE(canopy_type), INTENT(INOUT)          :: canopy ! vegetation variables

    !Local variables.
    REAL(r_2), DIMENSION(mp,ms+1)       :: at     ! coef "A" in finite diff eq
    REAL(r_2), DIMENSION(mp,ms+1)       :: bt     ! coef "B" in finite diff eq
    REAL(r_2), DIMENSION(mp,ms+1)       :: ct     ! coef "C" in finite diff eq
    REAL(r_2), DIMENSION(mp,ms+1)       :: rt

    INTEGER                             :: k,kk,i
    REAL(r_2), DIMENSION(mp,ms)         :: eff_por,old_wb  !effective porosity (mm3/mm3),wb(mm3/mm3),mass (mm) of eff_por
    REAL(r_2), DIMENSION(mp,ms)         :: msliq,msice             !mass of the soil liquid and ice water
    REAL(r_2), DIMENSION(mp)            :: den
    REAL(r_2), DIMENSION(mp)            :: dne
    REAL(r_2), DIMENSION(mp)            :: num
    REAL(r_2), DIMENSION(mp)            :: qin
    REAL(r_2), DIMENSION(mp)            :: qout
    REAL(r_2), DIMENSION(mp)            :: dqidw0
    REAL(r_2), DIMENSION(mp)            :: dqidw1
    REAL(r_2), DIMENSION(mp)            :: dqodw0
    REAL(r_2), DIMENSION(mp)            :: dqodw1,dqodw2
    REAL(r_2), DIMENSION(mp)            :: s1,s2,tmpi,temp0,voleq1,tempi

    REAL(r_2), DIMENSION(mp,0:ms+1)        :: zimm
    REAL(r_2), DIMENSION(mp,ms)            :: zmm
        
    REAL(r_2), DIMENSION(mp)            :: GWzimm,xs,zaq,s_mid
    REAL(r_2), DIMENSION(mp)            :: xs1,GWmsliq!xsi    !mass (mm) of liquid over/under saturation, mass of aquifer water
    REAL(r_2)                           :: xsi
    REAL(r_2), DIMENSION(mp,ms+1)       :: del_wb
    !MD DEBUG VARS
    INTEGER :: imp,ims,k_drain

    zimm(:,0) = 0._r_2
    ! MMY@23Apr2023 calcuate soil layer boundary    
    DO k=1,ms
       zimm(:,k) = zimm(:,k-1) + soil%zse_vec(:,k)*m2mm 
    END DO

    ! MMY@23Apr2023 calcuate the middle point of each soil layer
    zmm(:,1:ms)  = zimm(:,1:ms) - 0.5*soil%zse_vec(:,1:ms)*m2mm

    DO i=1,mp
       ! MMY@23Apr2023 calcuate the bottom boundary of aquifer and the middle point of the aquifer
       GWzimm(i) = zimm(i,ms)+soil%GWdz(i)*m2mm  
       zaq(i)    = zimm(i,ms) + 0.5_r_2*soil%GWdz(i)*m2mm
    END DO

    ! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
    ! preset to allow for non-land & snow points in trimb
    DO k=1,ms
       DO i=1,mp
          old_wb(i,k) = ssnow%wb(i,k)
          rt(i,k) = 0._r_2
          at(i,k) = 0._r_2
          bt(i,k) = 0._r_2
          ct(i,k) = 0._r_2
       END DO
    END DO

    !> soil hydraulic state/props                     
    CALL calc_soil_hydraulic_props(ssnow,soil,veg)    
                                                      
    !> equilibrium water content                      
    CALL calc_equilibrium_water_content(ssnow,soil)        

    !> calculate the subsurface runoff 
    CALL subsurface_drainage(ssnow,soil,veg)

    !* Calculate soil moisture for soil column and aquifer
    k = 1     !top soil layer
    DO i=1,mp
       qin(i)     = ssnow%sinfil(i)
       den(i)     = 0.5*(soil%zse_vec(i,k)+soil%zse_vec(i,k+1) )*m2mm 
       dne(i)     = (ssnow%zq(i,k+1)-ssnow%zq(i,k))
       num(i)     = (ssnow%smp(i,k+1)-ssnow%smp(i,k)) - dne(i)
       qout(i)    = -ssnow%hk(i,k)*num(i)/den(i)
       dqodw1(i)  = -(-ssnow%hk(i,k)*ssnow%dsmpdw(i,k)   + num(i)*ssnow%dhkdw(i,k))/den(i)
       dqodw2(i)  = -( ssnow%hk(i,k)*ssnow%dsmpdw(i,k+1) + num(i)*ssnow%dhkdw(i,k))/den(i)
       rt(i,k) =  qin(i) - qout(i)
       at(i,k) =  0._r_2
       bt(i,k) =  m2mm*soil%zse_vec(i,k)/dels + dqodw1(i) 
       ct(i,k) =  dqodw2(i)
    END DO
    DO k = 2, ms - 1     !middle soil layers
       DO i=1,mp

          den(i)     = 0.5*(soil%zse_vec(i,k)+soil%zse_vec(i,k-1) )*m2mm 
          dne(i)     = (ssnow%zq(i,k)-ssnow%zq(i,k-1))
          num(i)     = (ssnow%smp(i,k)-ssnow%smp(i,k-1)) - dne(i)
          qin(i)     = -ssnow%hk(i,k-1)*num(i)/den(i)
          dqidw0(i)  = -(-ssnow%hk(i,k-1)*ssnow%dsmpdw(i,k-1) + num(i)*ssnow%dhkdw(i,k-1))/den(i)
          dqidw1(i)  = -( ssnow%hk(i,k-1)*ssnow%dsmpdw(i,k)   + num(i)*ssnow%dhkdw(i,k-1))/den(i)
          den(i)     = 0.5*(soil%zse_vec(i,k)+soil%zse_vec(i,k+1) )*m2mm 
          dne(i)     = (ssnow%zq(i,k+1)-ssnow%zq(i,k))
          num(i)     = (ssnow%smp(i,k+1)-ssnow%smp(i,k)) - dne(i)
          qout(i)    = -ssnow%hk(i,k)*num(i)/den(i)
          dqodw1(i)  = -(-ssnow%hk(i,k)*ssnow%dsmpdw(i,k)   + num(i)*ssnow%dhkdw(i,k))/den(i)
          dqodw2(i)  = -( ssnow%hk(i,k)*ssnow%dsmpdw(i,k+1) + num(i)*ssnow%dhkdw(i,k))/den(i)
          rt(i,k) =  qin(i) - qout(i)
          at(i,k) = -dqidw0(i)
          bt(i,k) =  m2mm*soil%zse_vec(i,k)/dels - dqidw1(i) + dqodw1(i) 
          ! MMY ??? why the sign od bt is different to CLM5's
          ct(i,k) =  dqodw2(i)
       END DO
    END DO

    k = ms   !Bottom soil layer
    DO i=1,mp

       den(i)     = 0.5*(soil%zse_vec(i,k)+soil%zse_vec(i,k-1) )*m2mm 
       dne(i)     = (ssnow%zq(i,k)-ssnow%zq(i,k-1))
       num(i)     = (ssnow%smp(i,k)-ssnow%smp(i,k-1)) - dne(i)
       qin(i)     = -ssnow%hk(i,k-1)*num(i)/den(i)
       dqidw0(i)  = -(-ssnow%hk(i,k-1)*ssnow%dsmpdw(i,k-1) + num(i)*ssnow%dhkdw(i,k-1))/den(i)
       dqidw1(i)  = -( ssnow%hk(i,k-1)*ssnow%dsmpdw(i,k)   + num(i)*ssnow%dhkdw(i,k-1))/den(i)
       den(i)     = zaq(i) - zmm(i,k)
       dne(i)     = (ssnow%GWzq(i)-ssnow%zq(i,k))
       num(i)     =  (ssnow%GWsmp(i)-ssnow%smp(i,k)) - dne(i)
       qout(i)    = 0._r_2
       dqodw1(i)  = 0._r_2
       dqodw2(i)  = 0._r_2
       rt(i,k) =  qin(i) - qout(i)
       at(i,k) = -dqidw0(i)
       bt(i,k) =  m2mm*soil%zse_vec(i,k)/dels - dqidw1(i) + dqodw1(i) 
       ct(i,k) =  dqodw2(i)
    END DO

    CALL aquifer_recharge(dels,ssnow,soil,veg)

    CALL trimb(at,bt,ct,rt,ms)                       !use the default cable tridiag solution

    DO k=1,ms
       DO i=1,mp
          ssnow%wbliq(i,k) = ssnow%wbliq(i,k) + rt(i,k) - ssnow%qhlev(i,k)*dels/(m2mm*soil%zse_vec(i,k))   !volutermic liquid 
       END DO
    END DO

    DO i=1,mp
       ssnow%wbliq(i,ms) = ssnow%wbliq(i,ms) - ssnow%Qrecharge(i)*dels/(m2mm*soil%zse_vec(i,ms)) 
    END DO
    DO i=1,mp
       ssnow%GWwb(i) = ssnow%GWwb(i)  +  (ssnow%Qrecharge(i)-ssnow%qhlev(i,ms+1))*dels/(m2mm*soil%GWdz(i)) 
    END DO

    !determine the available pore space
    !volumetric
    DO k=1,ms
       DO i=1,mp
          eff_por(i,k)  = max(0._r_2, soil%ssat_vec(i,k) - den_rat * ssnow%wbice(i,k) )
       END DO
    END DO

    DO i=1,mp
       xsi = 0._r_2

       IF (ssnow%GWwb(i) .GT. soil%GWssat_vec(i)) THEN
          xsi = (ssnow%GWwb(i) - soil%GWssat_vec(i))*m2mm*soil%GWdz(i) 
          ssnow%GWwb(i) = soil%GWssat_vec(i)
       END IF

       DO k=1,ms
          IF (ssnow%wbliq(i,k) .GT. eff_por(i,k)) THEN
             xsi = xsi + (ssnow%wbliq(i,k) - eff_por(i,k))*(m2mm*soil%zse_vec(i,k)) 
             ssnow%wbliq(i,k) = eff_por(i,k)
          END IF
       END DO

       DO k = ms,1,-1  !loop from bottom to top adding extra water to each layer
          IF (xsi .GT. 0._r_2) THEN
             if (xsi .lt. (eff_por(i,k)-ssnow%wbliq(i,k))*(m2mm*soil%zse_vec(i,k))) then  
                ssnow%wbliq(i,k) = ssnow%wbliq(i,k) + xsi/(m2mm*soil%zse_vec(i,k))
                xsi = 0._r_2
             ELSE
                xsi = xsi - (eff_por(i,k) - ssnow%wbliq(i,k))*(m2mm*soil%zse_vec(i,k)) 
                ssnow%wbliq(i,k) = eff_por(i,k)
             END IF
          END IF
       END DO  !ms loop

       IF (xsi .GT. 0._r_2) THEN
          ssnow%qhz(i) = ssnow%qhz(i) + xsi/dels
          xsi = 0._r_2
       END IF

       ! MMY use watr to replace volwatmin to avoid questionable smp or hk (when wbliq<watr)
       ! MMY@23Apr2023, I change volwatmin to watr to avoid problems in smp or hk when wbliq<watr
       
       DO k = 1,ms
          xsi = 0._r_2             !should be a single float (array not needed)
          if (ssnow%wbliq(i,k) .lt. soil%watr(i,k)) then         ! MMY         
             xsi = (soil%watr(i,k) - ssnow%wbliq(i,k))*(m2mm*soil%zse_vec(i,k)) ! MMY
             ssnow%wbliq(i,k) = soil%watr(i,k)   ! MMY

             IF (k .LT. ms) THEN
                ssnow%wbliq(i,k+1) = ssnow%wbliq(i,k+1) - xsi/(m2mm*soil%zse_vec(i,k+1)) 
             ELSE
                ssnow%GWwb(i) = ssnow%GWwb(i) - xsi / (m2mm*soil%GWdz(i)) 
             END IF
          END IF
       END DO  !ms loop

       ! MMY@23Apr2023, I change volwatmin to watr to avoid problems in smp or hk when wbliq<watr
       if ( (ssnow%GWwb(i) .lt. soil%GWwatr(i)) .and. (soil%isoilm(i) .ne. 9) ) then ! MMY  
          xsi = (soil%GWwatr(i) - ssnow%GWwb(i)) / (m2mm*soil%GWdz(i))  !mm ! MMY
          ssnow%GWwb(i) = soil%GWwatr(i) ! MMY
          ssnow%qhz(i) = ssnow%qhz(i) - xsi / dels
          if (ssnow%qhz(i) .lt. 0.) print *, " MMY ===> Soil is too dry, aquifer extracts water from lateral drainage, found in SUBROUTINE smoistgw"
                                ! MMY@23Apr2023, it's an alarm for doubtful low aquifer moisture
       END IF

    END DO

    DO k=1,ms
       DO i=1,mp

          !update mass variables
          ssnow%wmliq(i,k)      = ssnow%wbliq(i,k) * &
               soil%zse_vec(i,k)*real(Cdensity_liq,r_2)
          ssnow%wmice(i,k)      = ssnow%wbice(i,k) * &
               soil%zse_vec(i,k)*real(Cdensity_ice,r_2)
          ssnow%wb(i,k)         = ssnow%wbliq(i,k) + den_rat * ssnow%wbice(i,k)
          ssnow%wmtot(i,k)      = ssnow%wmliq(i,k) + ssnow%wmice(i,k)
       END DO
    END DO

    !> give value to subsurface runoff
    DO i=1,mp
       ssnow%rnof2(i)        = ssnow%qhz(i)               !rnof2 is default cable deep runoff var
    END DO  !mp loop


  END SUBROUTINE smoistgw

  SUBROUTINE soil_snow_gw(dels, soil, ssnow, canopy, met, bal, veg)
  !*## Purpose
  !
  !  This is the main subroutine for Mark Decker's new hydrology scheme. 
  !  This subroutine should be called in subroutine cbm but not yet.
  ! 
  !

  ! Inputs:
  !	 dt_in - time step in sec
  !	 ktau_in - time step no.
  !	 ga	 - ground heat flux W/m^2
  !	 dgdtg	 -
  !	 condxpr - total precip reaching the ground (liquid and solid)
  !	 scondxpr - precip (solid only)
  !	 fev   - transpiration (W/m2)
  !	 fes   - soil evaporation (W/m2)
  !	 isoil - soil type
  !	 ivegt - vegetation type
  ! Output
  !	 ssnow

    USE cable_common_module
    USE snow_processes_soil_thermal_mod, ONLY : snow_processes_soil_thermal ! inserted by rk4417 - phase2
    
    REAL                     , INTENT(IN)     :: dels ! integration time step (s)
    TYPE(soil_parameter_type), INTENT(INOUT)  :: soil
    TYPE(soil_snow_type)     , INTENT(INOUT)  :: ssnow
    TYPE(canopy_type)        , INTENT(INOUT)  :: canopy
    TYPE(veg_parameter_type) , INTENT(INOUT)  :: veg
    TYPE(met_type)           , INTENT(INOUT)  :: met ! all met forcing
    TYPE (balances_type)     , INTENT(INOUT)  :: bal

    INTEGER             :: k,i
   !   REAL, DIMENSION(mp) :: snowmlt ! MMY@Nov2022 not really use
   !   REAL, DIMENSION(mp) :: GWwb_ic ! MMY@Nov2022 not really use
    REAL, DIMENSION(mp) :: tggsn_old,del_wbtot ! wbtot_ic ! MMY@Nov2022 not really use
    REAL(r_2), DIMENSION(mp) :: xx
    REAL(r_2), DIMENSION(mp,ms) :: gammzz_snow  
    REAL                :: zsetot
    INTEGER, SAVE :: ktau =0
    REAL(r_2) :: wb_lake_T, rnof2_T
    LOGICAL :: use_sli
    LOGICAL, SAVE :: first_gw_hydro_call = .TRUE.

    use_sli = .FALSE.
    ktau = ktau +1
    zsetot = SUM(soil%zse)
    ssnow%tggav = 0.

    !>## Order of procedure
    !> 1. Set the physic constants 

    !> 2. Calculate average soil temp for soil column  
    DO k = 1, ms
       ssnow%tggav = ssnow%tggav  + soil%zse(k)*ssnow%tgg(:,k)/zsetot
    END DO

    !> 3. If it is the first time step, define the ratio of ice density to liquid water density
    !     and constrains wb and wbice
    if (first_gw_hydro_call) then
       den_rat = real(Cdensity_ice/Cdensity_liq,r_2) ! MMY @Nov2022
       ! call set_den_rat() ! MMY @Nov2022
       DO k = 1, ms
          ssnow%wb(:,k)    = MIN(soil%ssat_vec(:,k), MAX(real(ssnow%wb(:,k)), soil%watr(:,k)))
          ssnow%wbice(:,k) = MIN(real(ssnow%wb(:,k))/den_rat, real(ssnow%wbice(:,k)))
       END DO
    end if

   !> 4. set a default snow depth in the top snow layer
    IF( cable_runtime%offline .OR. cable_runtime%mk3l ) ssnow%t_snwlr = 0.05_r_2

    !> 5. initialize hydrology variables
    ssnow%otgg = ssnow%tgg      ! initialize of ssnow%otgg
    DO i=1,mp
       ssnow%fwtop1(i) = 0.0
       ssnow%fwtop2(i) = 0.0
       ssnow%fwtop3(i) = 0.0
       ssnow%runoff(i) = 0.0 ! initialise total runoff
       ssnow%rnof1(i) = 0.0 ! initialise surface runoff
       ssnow%rnof2(i) = 0.0 ! initialise deep drainage
       ssnow%smelt(i) = 0.0 ! initialise snowmelt
       ssnow%dtmlt(i,:) = 0.0 ! initialise water flux to the soil
       ssnow%osnowd(i) = ssnow%snowd(i)
       ! Scaling  runoff to kg/m^2/s (mm/s) to match rest of the model
       ssnow%sinfil(i) = 0.0
       ssnow%qhz(i) = 0.0
    END DO

    !> 6. set heat cap lower limit
    IF (cable_user%soil_thermal_fix) THEN
!$       soil%heat_cap_lower_limit(:,:) = 0.01  !never allow /0   ! FEEDBACK (MMY asks: I guess we should delete this line since someone changed it on purpose) --rk4417
       soil%heat_cap_lower_limit(:,:) = 0._r_2  !allow /0 to show bugs  ! FEEDBACK (MMY asks: I don't know how it shows bugs, can anyone check it???) --rk4417
    ELSE
       print *, "MMY testing soil%css_vec(:,:) =",soil%css_vec(:,:), "soil%rhosoil_vec(:,:) = ", soil%rhosoil_vec(:,:) !MMY@18May2023
       soil%heat_cap_lower_limit(:,:) = soil%css_vec(:,:) * soil%rhosoil_vec(:,:)
    END IF

     !> 7. MMY???
    IF( (.NOT.cable_user%cable_runtime_coupled ) .AND. (first_gw_hydro_call)) THEN

       IF (cable_runtime%um) canopy%dgdtg = 0.0 ! RML added um condition  

! MMY@23Apr2023 delete this block, since without it CABLE-GW works well    ! FEEDBACK (Please see MMY questions regarding commented block below) --rk4417
! MMY@13Mar2023 the block below is an initialization for ssnow%wb, ssnow%wbice & ssnow%snowd (if soil%isoilm=9)
!               It is duplicate to the section 3. I don't think we need to initializate ssnow%wb and ssnow%wbice again.
!               However, I don't know whether we should keep ssnow%snowd = max_glacier_snowd for soil%isoilm=9.
!               Please check with Claire and Jhan if possible.

!$       ! MMY??? should we keep wb & wbice setting for soil%isoilm==9 (permanent ice) and snow covering region
!$
!$       ! after discussion with BP                                     ! block commented out by rk4417 as missing from MMY
!$       ! N.B. snmin should exceed sum of layer depths, i.e. .11 m     ! need to discuss with MMY
!$       ssnow%wbtot = 0.0
!$       ssnow%wb(:,:)  = MIN( soil%ssat_vec(:,:), MAX ( ssnow%wb(:,:), 0.5*soil%swilt_vec(:,:) ) )
!$
!$       DO k = 1, ms
!$
!$          WHERE( ssnow%tgg(:,k) <= CTFRZ .AND. ssnow%wbice(:,k) <= 0.001*ssnow%wb(:,k) )   &
!$               ssnow%wbice(:,k) = 0.5 * ssnow%wb(:,k)
!$
!$          !WHERE( ssnow%tgg(:,k) < CTFRZ)                                    &
!$          !   ssnow%wbice(:,k) = 0.8 * ssnow%wb(:,k)
!$
!$       END DO
!$       WHERE ( soil%isoilm .EQ. 9)! .and. ssnow%snowd .le. 0.1*max_glacier_snowd)
!$
!$          ! permanent ice: fix hard-wired number in next version
!$          ssnow%snowd = max_glacier_snowd
!$          ssnow%osnowd = max_glacier_snowd
!$          ssnow%tgg(:,1) = ssnow%tgg(:,1) - 1.0
!$
!$       END WHERE
!$
!$       WHERE ( SPREAD(soil%isoilm,2,ms) .EQ. 9 )
!$
!$          ssnow%wb    = 0.95 * soil%ssat_vec
!$          ssnow%wbice = 0.95 * ssnow%wb
!$
!$       END WHERE
! END DELETE
    END IF

   !> 8. Calculate snow heat capacity to the top soil
     ! ??? MMY@Nov2022 gammzz_snow and ssnow%gammzz calculated here are useless, should be commented out ___
    gammzz_snow(:,:) = 0._r_2      ! MMY@23Apr2023 necessary to have this line
                                   ! since only the top soil layer can have snow
                                   ! and gammzz_snow in the other layers should
                                   ! be 0
    do i=1,mp
      ! ssnow%isflag = 0 one snow layer; =1 three snow layers
       gammzz_snow(i,1) = real((1. - ssnow%isflag(i)) * Ccgsnow * ssnow%snowd(i),r_2)
    end do

   !> 9. Find the mass and saturation fraction of ice and liq from the prognostic volumetric values (wb,wbice)
   !     and calculate sucs_hys and smp_hys
    DO k=1,ms
       DO i=1,mp
          ssnow%wbliq(i,k) = ssnow%wb(i,k) - den_rat*ssnow%wbice(i,k)  ! MMY      !liquid volume
          ssnow%wmice(i,k) = ssnow%wbice(i,k)*real(Cdensity_ice,r_2)*soil%zse_vec(i,k)!ice mass
          ssnow%wmliq(i,k) = ssnow%wbliq(i,k)*real(Cdensity_liq,r_2)*soil%zse_vec(i,k) !liquid mass
          ssnow%wmtot(i,k) = ssnow%wmice(i,k) + ssnow%wmliq(i,k)                  !liq+ice mass

          ssnow%wblf(i,k)   = MAX(ssnow%wbliq(i,k)/soil%ssat_vec(i,k),0.01_r_2)
          ssnow%wbfice(i,k) = MAX(ssnow%wbice(i,k)/soil%ssat_vec(i,k),0._r_2)
         !do not pass with mpi  
         ssnow%sucs_hys(i,k) = ssnow%hys_fac(i,k)*soil%sucs_vec(i,k)
         ssnow%smp_hys(i,k)  = min(ssnow%smp_hys(i,k),-ssnow%sucs_hys(i,k))
       END DO
    END DO

    !> 10. calculate heat capacity for each soil layer
    IF( first_gw_hydro_call ) THEN
       DO k=1,ms                       
          DO i=1,mp
             ssnow%gammzz(i,k) = max(soil%heat_cap_lower_limit(i,k),&
                  (1.0-soil%ssat_vec(i,k))*&
                  soil%css_vec(i,k) * soil%rhosoil_vec(i,k)  &
                  + ssnow%wbliq(i,k) * real(Ccswat*Cdensity_liq,r_2)  &
                  + ssnow%wbice(i,k) * real(Ccsice*Cdensity_ice,r_2) )* &
                  soil%zse_vec(i,k) +   gammzz_snow(i,k)
          END DO
       END DO                          
    ENDIF  ! if(.NOT.cable_runtime_coupled) and first_gw_hydro_call

  !>  wbliq_old is used for hysteresis 
   ssnow%wbliq_old = ssnow%wbliq       

   !> 10. snow processes and thermal soil 
    CALL snow_processes_soil_thermal(dels,ssnow,soil,veg,canopy,met,bal) 
    
   !> 11. transpiration loss per soil layer ! leave here for now, could move into soilsnow as well
    CALL remove_transGW(dels, soil, ssnow, canopy, veg)

   !> 12. Snow freezes and melts. 
    CALL  GWsoilfreeze(dels, soil, ssnow)           
    
    ssnow%fwtop = canopy%precis/dels + ssnow%smelt/dels   !water from canopy and snowmelt [mm/s]

    !> 13. calculate water table depth 
    CALL iterative_wtd (ssnow, soil, veg, .TRUE. )

    !> 14. surface runoff, incorporate ssnow%pudsto?
    CALL ovrlndflx (dels, ssnow, soil, veg, canopy,use_sli )
   !> remove soil evap from throughfall
    ssnow%sinfil = ssnow%fwtop - canopy%segg  !canopy%fes/CHL

    !> 15. vertical soil moisture movement.
    CALL smoistgw (dels,ktau,ssnow,soil,veg,canopy)

    ! _________ MMY@23Apr2023 need to check these hysteresis-related codes, I think they should show before smoistgw ______
    !* if Brook & Cosby water retention hysteresis is used, calculate smp_hys, wb_hys, 
    !  ssat_hys, hys_fac, sucs_hys, watr_hys...
   IF (gw_params%BC_hysteresis)  &                              
             CALL swc_hyst_direction(soil,ssnow,veg)

   !call swc_smp_dsmpdw(soil,ssnow)                       
   !> 16. soil matric potential 
   if (gw_params%BC_hysteresis) then
      call brook_corey_hysteresis_swc_smp(soil,ssnow)
   elseif (gw_params%HC_SWC) then
      call hutson_cass_swc_smp(soil,ssnow)
   else
      call brook_corey_swc_smp(soil,ssnow)
   end if
    
    !> 17. correction required for energy balance in online simulations
    IF( cable_runtime%um ) THEN

       !cls package - rewritten for flexibility
       canopy%fhs_cor = ssnow%dtmlt(:,1)*ssnow%dfh_dtg
       !canopy%fes_cor = ssnow%dtmlt(:,1)*(ssnow%dfe_ddq * ssnow%ddq_dtg)
       canopy%fes_cor = ssnow%dtmlt(:,1)*ssnow%dfe_dtg

       canopy%fhs = canopy%fhs+canopy%fhs_cor
       canopy%fes = canopy%fes+canopy%fes_cor

       !REV_CORR associated changes to other energy balance terms
       !NB canopy%fns changed not rad%flws as the correction term needs to
       !pass through the canopy in entirety, not be partially absorbed
       IF (cable_user%L_REV_CORR) THEN
          canopy%fns_cor = ssnow%dtmlt(:,1)*ssnow%dfn_dtg
          canopy%ga_cor = ssnow%dtmlt(:,1)*canopy%dgdtg

          canopy%fns = canopy%fns + canopy%fns_cor
          canopy%ga = canopy%ga + canopy%ga_cor

          canopy%fess = canopy%fess + canopy%fes_cor
       ENDIF
    ENDIF
    
   !> 18. update variables ! MMY??? where is pudsto calculated
    DO i=1,mp
       ssnow%pudsto(i) = 0.0  !no puddle
       ssnow%smelt(i)  = ssnow%smelt(i)/dels    !change units to mm/s.  cable_driver then reverts back to mm
       ssnow%runoff(i) = (ssnow%rnof1(i) + ssnow%rnof2(i))!*dels  !cable_driver converts from mm/s to mm
       !rnof1 and rnof2 are already in mm/s
       ! Set weighted soil/snow surface temperature
       ssnow%tss(i) =  (1-ssnow%isflag(i))*ssnow%tgg(i,1) + ssnow%isflag(i)*ssnow%tggsn(i,1)

       !total water mass at the end of the soilsnow_GW routine
       ssnow%wbtot(i)  = SUM(ssnow%wbliq(i,:)*Cdensity_liq*soil%zse(:),dim=1) + &
            SUM(ssnow%wbice(i,:)*Cdensity_ice*soil%zse(:),dim=1) + &
            ssnow%GWwb(i)*soil%GWdz(i)*Cdensity_liq

       !for debug water balance.  del_wbtot = fluxes = infiltration [though-evap] - trans - qhorz drainage
       del_wbtot(i)   = dels * (ssnow%sinfil(i) - ssnow%rnof2(i) - canopy%fevc(i) / CHL)

    END DO
    ! MMY take from subroutine soil_snow, hydraulic redistribution should be included
    !  IF( redistrb )                                                              &
    !       CALL hydraulic_redistribution( dels, soil, ssnow, canopy, veg, met )
    first_gw_hydro_call=.false. 
    
  END SUBROUTINE soil_snow_gw

  SUBROUTINE calc_equilibrium_water_content(ssnow,soil)
   !* Calculate soil moisture and potential at hydrostatic equilibrium
   !  with water table depth [Zeng and Decker 2009](https://doi.org/10.1175/2008JHM1011.1)
   ! MMY unfinished

    IMPLICIT NONE

    TYPE (soil_snow_type), INTENT(INOUT)      :: ssnow ! soil and snow variables
    TYPE (soil_parameter_type), INTENT(INOUT)    :: soil  ! soil parameters
    !local variables
    REAL(r_2), DIMENSION(mp)    :: zaq      !node depth of the aquifer
    REAL(r_2), DIMENSION(ms)    :: dzmm     !layer thickness for single tile
    REAL(r_2), DIMENSION(mp)    :: GWdzmm   !aquifer thickness at each tile
    REAL(r_2), DIMENSION(mp)    :: GWzimm   !aquifer layer interface depth
    REAL(r_2), dimension(mp,0:ms)  :: zimm     !layer interface depth in mm
    REAL(r_2), dimension(mp,ms)    :: zmm      !node depths in mm
    REAL(r_2)                   :: tempi, temp0,voleq1,wbrat,&
                                   zi_smpc,tmp_const,voleq2
    INTEGER :: k,i

!$    IF (gw_params%ssgw_ice_switch) THEN  ! FEEDBACK (MMY asks: ask Claire or Anna what ssgw_ice_switch is for and whether to keep it) --rk4417  
!$       smp_cor = 8.0
!$    ELSE
!$       smp_cor = 0.0
!$    END IF

    !make code cleaner define these here
    zimm(:,:) = 0._r_2
    DO k=1,ms
       zimm(:,k) = zimm(:,k-1) + m2mm*soil%zse_vec(:,k)        
       zmm(:,k)  = zimm(:,k-1) + 0.5_r_2*m2mm*soil%zse_vec(:,k)
    END DO

!$    IF (.NOT.gw_params%ssgw_ice_switch) THEN       ! FEEDBACK (MMY asks: ask Claire or Anna what ssgw_ice_switch is for and whether to keep it) --rk4417  
!$       ice_correction(:,:) = 1._r_2
!$
!$    ELSE
!$
!$       DO k=1,ms
!$          DO i=1,mp
!$             ice_correction(i,k)    = 1._r_2 + smp_cor * ssnow%wbice(i,k)
!$             ice_correction(i,k)    =  ice_correction(i,k)**(2.0/soil%bch_vec(i,k))
!$          END DO
!$       END DO
!$       DO i=1,mp
!$          ice_correction(i,ms+1)    = 1._r_2 + smp_cor * ssnow%wbice(i,ms)
!$          ice_correction(i,ms+1)    =  ice_correction(i,ms+1)**(2.0/soil%GWbch_vec(i))
!$       END DO
!$
!$    END IF

    do i=1,mp                                     
       GWzimm(i) = zimm(i,ms)+m2mm*soil%GWdz(i)   
       zaq(i)    = zimm(i,ms) + 0.5_r_2*m2mm*soil%GWdz(i)
    end do

    soil%wbc_GW(:) = 0.0
    soil%wbc_vec(:,:) = 0.0
    soil%smpc_vec(:,:) = 1.0e+30
    soil%smpc_GW(:) = 1.0e+30

    if (.not.gw_params%BC_hysteresis) then !not needed but make code clear
       ssnow%watr_hys = soil%watr(:,:)
       ssnow%ssat_hys = soil%ssat_vec(:,:)
       ssnow%sucs_hys = soil%sucs_vec

       if (gw_params%HC_SWC) then
         soil%wbc_vec(:,:) = 2.0*soil%bch_vec(:,:)*soil%ssat_vec(:,:)/&
                                 (1.0+2.0*soil%bch_vec(:,:))
         soil%smpc_vec(:,:) = -soil%sucs_vec(:,:) * &
                               (2.0*soil%bch_vec(:,:)/&
                               ((1.0+2.0*soil%bch_vec(:,:))))**(-soil%bch_vec(:,:))
         soil%wbc_GW(:) = 2.0*soil%GWbch_vec(:)*soil%GWssat_vec(:)/&
                                 (1.0+2.0*soil%GWbch_vec(:))
         soil%smpc_GW(:) = -soil%GWsucs_vec(:) * &
                               (2.0*soil%GWbch_vec(:)/&
                               ((1.0+2.0*soil%GWbch_vec(:))))**(-soil%GWbch_vec(:))
       end if
    end if
    
    !equilibrium water content
    DO k=1,ms
       DO i=1,mp

          if ((ssnow%wtd(i) .le. zimm(i,k-1))) then         !fully saturated        ! replaced block above by below as per MMY -- rk4417

             ssnow%wbeq(i,k) = ssnow%ssat_hys(i,k)

          elseif ((ssnow%wtd(i) .le. zimm(i,k)) .and. &
                  (ssnow%wtd(i) .gt. zimm(i,k-1))) then

             tempi = 1._r_2
             temp0 = &
                   (((ssnow%sucs_hys(i,k)+ssnow%wtd(i)-zimm(i,k-1))/&
                      ssnow%sucs_hys(i,k)))**(1._r_2-1._r_2/soil%bch_vec(i,k))
             voleq1 = -ssnow%sucs_hys(i,k)*(ssnow%ssat_hys(i,k)-ssnow%watr_hys(i,k))/&
                       (1._r_2-1._r_2/soil%bch_vec(i,k))/&
                       (ssnow%wtd(i)-zimm(i,k-1))*(tempi-temp0) + ssnow%watr_hys(i,k) ! MMY@23Apr2023 compared with trunk version, Mark added ssnow%watr_hys(i,k) to this equation, but why???
             ssnow%wbeq(i,k) = (voleq1*(ssnow%wtd(i)-zimm(i,k-1)) +& ! MMY@23Apr2023 compared with trunk version, Mark remove ssnow%watr_hys(i,k) from this equation but why???
                               (ssnow%ssat_hys(i,k))&
                               *(zimm(i,k)-ssnow%wtd(i)))/(zimm(i,k)-zimm(i,k-1))
             ! MMY@23Apr2023 compared with trunk version, Mark removed ssnow%wbeq(i,k) = ssnow%wbeq(i,k)*ice_correction(i,k), why???
          else

             tempi = (((ssnow%sucs_hys(i,k)+ssnow%wtd(i)-zimm(i,k))/&
                      ssnow%sucs_hys(i,k)))**(1._r_2-1._r_2/soil%bch_vec(i,k))
             temp0 = (((ssnow%sucs_hys(i,k)+ssnow%wtd(i)-zimm(i,k-1))/&
                     ssnow%sucs_hys(i,k)))**(1._r_2-1._r_2/soil%bch_vec(i,k))
             ssnow%wbeq(i,k) = -ssnow%sucs_hys(i,k)*(ssnow%ssat_hys(i,k)-ssnow%watr_hys(i,k))/&
                               (1._r_2-1._r_2/soil%bch_vec(i,k))/&
                               (zimm(i,k)-zimm(i,k-1))*(tempi-temp0)+ssnow%watr_hys(i,k)
             ! MMY@23Apr2023 compared with trunk version, Mark removed ssnow%wbeq(i,k) = ssnow%wbeq(i,k)*ice_correction(i,k), why???
          end if

         if (.not.gw_params%HC_SWC) then
             ssnow%wbeq(i,k) = min(max(ssnow%wbeq(i,k),ssnow%watr_hys(i,k)),ssnow%ssat_hys(i,k))

             wbrat = min(max((&
                     ssnow%wbeq(i,k) - ssnow%watr_hys(i,k))/&
                             (ssnow%ssat_hys(i,k)-ssnow%watr_hys(i,k)),&
                          0.01_r_2),1._r_2)
              ssnow%zq(i,k) = max(&
                            -ssnow%sucs_hys(i,k)*(wbrat**(-soil%bch_vec(i,k))),sucmin)

          else
             if (ssnow%wbeq(i,k) .lt. soil%wbc_vec(i,k)) then
               wbrat = min(max((&
                       ssnow%wbeq(i,k) - soil%watr(i,k))/(soil%ssat_vec(i,k)-soil%watr(i,k)),&
                               0.01_r_2),1._r_2)

               ssnow%zq(i,k) = max(&
                                 -soil%sucs_vec(i,k)*(wbrat**(-soil%bch_vec(i,k))),sucmin)
             else
                ssnow%zq(i,k) = -soil%sucs_vec(i,k)* &
                             sqrt(1._r_2 - ssnow%wbeq(i,k)/soil%ssat_vec(i,k))/&
                                  (sqrt(1._r_2-soil%wbc_vec(i,k)/soil%ssat_vec(i,k))*&
                                 ( (soil%wbc_vec(i,k)/soil%ssat_vec(i,k))**soil%bch_vec(i,k) ))

             end if

          end if

       END DO  !mp
    END DO  !ms

    DO i=1,mp

       if (ssnow%wtd(i) .le. zimm(i,ms)) then  !fully saturated  !MMY@23Apr2023 I don't know why Mark changed it but as codes from CABLE-GW works, I suggest to delete the codes from trunk 

          ssnow%GWwbeq(i) = soil%GWssat_vec(i)   ! MMY@23Apr2023 in trunk it has '-soil%GWwatr(i)'

       elseif ((ssnow%wtd(i) .gt. GWzimm(i)))   then     !fully unsaturated

          tempi = &
                (((soil%GWsucs_vec(i)+ssnow%wtd(i)-GWzimm(i))/&
                  soil%GWsucs_vec(i)))**(1._r_2-1._r_2/soil%GWbch_vec(i))
          temp0 = (((soil%GWsucs_vec(i)+ssnow%wtd(i)-zimm(i,ms))/&
                     soil%GWsucs_vec(i)))**(1._r_2-1._r_2/soil%GWbch_vec(i))
          ssnow%GWwbeq(i) = -soil%GWsucs_vec(i)*soil%GWssat_vec(i)/&
                          (1._r_2-1._r_2/soil%GWbch_vec(i))/&
                           (GWzimm(i)-zimm(i,ms))*(tempi-temp0) + soil%GWwatr(i)

       else

             tempi = 1._r_2
             temp0 = &
                   (((soil%GWsucs_vec(i)+ssnow%wtd(i)-zimm(i,ms))/&
                      soil%GWsucs_vec(i)))**(1._r_2-1._r_2/soil%GWbch_vec(i))
             voleq1 = -soil%GWsucs_vec(i)*(soil%GWssat_vec(i)-soil%GWwatr(i))/&
                       (1._r_2-1._r_2/soil%GWbch_vec(i))/&
                       (ssnow%wtd(i)-zimm(i,ms))*(tempi-temp0) + soil%GWwatr(i)
             ssnow%GWwbeq(i) = (voleq1*(ssnow%wtd(i)-zimm(i,ms)) +& ! MMY@23Apr2023 this block doesn't consider 'soil%GWwatr(i))' as the trunk why??? 
                               (soil%GWssat_vec(i))&
                               *(GWzimm(i)-ssnow%wtd(i)))/(GWzimm(i)-zimm(i,ms))

       end if

       if (.not.gw_params%HC_SWC) then

          ssnow%GWwbeq(i) = min(max(ssnow%GWwbeq(i),soil%GWwatr(i)),soil%GWssat_vec(i))

          ssnow%GWzq(i) = -soil%GWsucs_vec(i)*(max((ssnow%GWwbeq(i)-soil%GWwatr(i))/     &
                       (soil%GWssat_vec(i)-soil%GWwatr(i)),0.01_r_2))**(-soil%GWbch_vec(i))
          ssnow%GWzq(i) = max(sucmin, ssnow%GWzq(i))

       else
          if (ssnow%GWwbeq(i) .lt. soil%wbc_GW(i)) then
            wbrat = min(max((&
                    ssnow%wbeq(i,k) - soil%GWwatr(i))/(soil%GWssat_vec(i)-soil%GWwatr(i)),&
                            0.01_r_2),1._r_2)

            ssnow%GWzq(i) = max(&
                              -soil%GWsucs_vec(i)*(wbrat**(-soil%GWbch_vec(i))),sucmin)
          else
             ssnow%gwzq(i) = -soil%gwsucs_vec(i)* &
                          sqrt(1._r_2 - ssnow%gwwbeq(i)/soil%gwssat_vec(i))/&
                               (sqrt(1._r_2-soil%wbc_gw(i)/soil%gwssat_vec(i))*&
                              ( (soil%wbc_gw(i)/soil%GWssat_vec(i))**soil%GWbch_vec(i) ))

          end if
        end if

    END DO

  END SUBROUTINE calc_equilibrium_water_content


  SUBROUTINE calc_srf_wet_fraction(ssnow,soil,met,veg)
    !* Calculate the wet fraction of the surafce
    !  following [Decker, 2015](http://doi.wiley.com/10.1002/2015MS000507)
  

    USE grid_constants_mod_cbl, ONLY : lakes_cable

    IMPLICIT NONE
    TYPE(soil_snow_type), INTENT(INOUT)      :: ssnow  ! soil+snow variables
    TYPE(soil_parameter_type), INTENT(IN)    :: soil ! soil parameters
    TYPE (met_type), INTENT(IN)       :: met
    TYPE (veg_parameter_type), INTENT(IN)    :: veg

    !local variables
    REAL(r_2), DIMENSION(mp)           :: icef,satfrac_liqice,S
    REAL(r_2)                          :: fice,xx
    REAL(r_2)                          :: liqmass,icemass,totmass
    INTEGER                            :: i,j,k
    REAL(r_2)                          :: wb_unsat,wb_lin,funcval
    REAL(r_2)                          :: derv,slopeSTDmm,func_step
    REAL(r_2)                          :: wb_evap_threshold
!    REAL(r_2)                          :: pi_temp=3.1415927 ! no need, use CPI instead - rk4417 - phase2

    !> if true, using Dani Or soil evaporation scheme
    IF (cable_user%or_evap) THEN

       CALL saturated_fraction(ssnow,soil,veg)

       ssnow%wetfac(:) = 1.0

       DO i=1,mp
          IF( ssnow%snowd(i) > 0.1) ssnow%wetfac(i) = 0.9

          IF ( veg%iveg(i) == lakes_cable .AND. met%tk(i) >= CTFRZ + 5. )   &
               ssnow%wetfac(i) = 1.0 

          IF( veg%iveg(i) == lakes_cable .AND. met%tk(i) < CTFRZ + 5. )   &
               ssnow%wetfac(i) = 0.7
       END DO

    ELSEIF (cable_user%gw_model) THEN
  !> if true, using Dani Or soil evaporation scheme
       CALL saturated_fraction(ssnow,soil,veg)


       DO i = 1,mp
         ! MMY@23Apr2023, calc_srf_wet_fraction is called before ssnow%wmice and ssnow%wmliq calculated, 
         !                here needs to calcuate icemass and liqmass. 

          icemass  = ssnow%wbice(i,1)*real(Cdensity_ice,r_2)*soil%zse_vec(i,1) !ice mass 
          liqmass  = ssnow%wbliq(i,1)*real(Cdensity_liq,r_2)*soil%zse_vec(i,1) 
          totmass  = MAX(liqmass+icemass,REAL(1e-2,r_2))
          icef(i)  = max(0._r_2,min(1._r_2,icemass / totmass)) ! MMY
       END DO

       !srf frozen fraction.  should be based on topography
       DO i = 1,mp
          fice = (EXP(-gw_params%IceAlpha*(1._r_2-icef(i)))-&
               EXP(-gw_params%IceAlpha))/&
               (1._r_2-EXP(-gw_params%IceAlpha))
          fice = MIN(1._r_2,MAX(0._r_2,fice))

          satfrac_liqice(i) = max(0.,min(0.95,fice + (1._r_2-fice)*ssnow%satfrac(i) ) ) ! MMY

          wb_unsat = ((ssnow%wb(i,1)-ssnow%wbice(i,1)*den_rat) -& ! MMY add *den_rat ! MMY@23Apr2023, since this subroutine is called before den_rat calculated in soil_snow_gw, here den_rat's value from definition at the start of module
               ssnow%satfrac(i)*soil%ssat_vec(i,1))/(1.-ssnow%satfrac(i))
          wb_unsat = MIN(soil%ssat_vec(i,1),MAX(0.,wb_unsat))

          wb_evap_threshold = MIN( MAX( &
               gw_params%SoilEvapAlpha*soil%sfc_vec(i,1), &
               soil%swilt_vec(i,1) ), soil%ssat_vec(i,1) )

          !Sakguchi and Zeng 2009
          IF (wb_unsat .GE. wb_evap_threshold) THEN
             xx = 1.
          ELSE
!             xx = 0.25 * (1._r_2 - cos(pi_temp*wb_unsat/(wb_evap_threshold)))**2.0 ! MMY@23Apr2023 add pi_temp to this subroutine, C=>phys which doesn't include pi value
! replaced above by below - rk4417 - phase2
             xx = 0.25 * (1._r_2 - cos(CPI*wb_unsat/(wb_evap_threshold)))**2.0 
          END IF

          ssnow%wetfac(i) = MAX(0.0,MIN(1.0,satfrac_liqice(i) +&
               (1. - satfrac_liqice(i))*xx ) )

       END DO

    ELSE  !Default formulation

       !call saturated_fraction(ssnow,soil,veg)
       ssnow%satfrac(:) = 1.0e-8
       ssnow%rh_srf(:)  = 1.0

       ssnow%wetfac = MAX( 1.e-6, MIN( 1.0,&
            ( REAL (ssnow%wb(:,1) ) - soil%swilt/ 2.0 )                  &
            / ( soil%sfc - soil%swilt/2.0 ) ) )

       DO i=1,mp

          IF( ssnow%wbice(i,1) > 0. )&
               ssnow%wetfac(i) = ssnow%wetfac(i) * &
               REAL(MAX( 0.5_r_2, 1._r_2 - MIN( 0.2_r_2, &
               ( ssnow%wbice(i,1) * den_rat & ! MMY not sure whether needed but add * den_rat
                                / ssnow%wb(i,1) )**2 ) ) )

          IF( ssnow%snowd(i) > 0.1) ssnow%wetfac(i) = 0.9

          IF ( veg%iveg(i) == lakes_cable .AND. met%tk(i) >= Ctfrz + 5. )   &
               ssnow%wetfac(i) = 1.0 

          IF( veg%iveg(i) == lakes_cable .AND. met%tk(i) < Ctfrz + 5. )   &
               ssnow%wetfac(i) = 0.7


       ENDDO
       ! owetfac introduced to reduce sharp changes in dry regions,
       ! especially in offline runs in which there may be discrepancies b/n
       ! timing of precip and temperature change (EAK apr2009)
       ssnow%wetfac = 0.5*(ssnow%wetfac + ssnow%owetfac)

    ENDIF  !or_evap, gw_model, or default wetfac parameterization

  END SUBROUTINE calc_srf_wet_fraction

  SUBROUTINE calc_soil_hydraulic_props(ssnow,soil,veg)
     !* Calculate soil hydraulic conductivity and dhk/dw

    USE cable_common_module
    TYPE(soil_parameter_type), INTENT(INOUT)     :: soil
    TYPE(soil_snow_type)     , INTENT(INOUT)  :: ssnow
    TYPE(veg_parameter_type) , INTENT(INOUT)     :: veg

    INTEGER :: i,k,kk

    REAL(r_2), DIMENSION(mp) :: s1, &  !temporary variables for calculating hydraulic properties
         s2, &
         s_mid, &
         liq_ratio, &
         Dliq_ratio_Dz

    REAL(r_2), DIMENSION(0:ms) :: zimm  !depths at interface between layers
    REAL(r_2), DIMENSION(mp,ms+1) ::wb_temp
    REAL(r_2), DIMENSION(mp,ms+1) :: hk_ice_factor

    !soil matric potential, hydraulic conductivity, and derivatives of each with respect to water (calculated using total (not liquid))
   ! __ MMY@23Apr2023 need to test, since itself cannot provide all hysteresis parameters and become redundant ____ 
   if (gw_params%BC_hysteresis) then                             
      !swc_smp_dsmpdw => brook_corey_hysteresis_swc_smp
      ssnow%sucs_hys(:,:) = ssnow%hys_fac(:,:)*soil%sucs_vec(:,:)
   elseif (gw_params%HC_SWC) then
      !swc_smp_dsmpdw => hutson_cass_swc_smp
      ssnow%sucs_hys(:,:) = soil%sucs_vec(:,:)
   else
      !swc_smp_dsmpdw => brook_corey_swc_smp
      ssnow%sucs_hys(:,:) = soil%sucs_vec(:,:)
   end if

   ! ___ MMY COMMENT: for Hutson Cass SWC potential ___  
   do k=1,ms
      do i=1,mp
         soil%wbc_vec(i,k) = 2.0*soil%bch_vec(i,k)*soil%ssat_vec(i,k)/&
                                 (1.0+2.0*soil%bch_vec(i,k))
         soil%smpc_vec(i,k) = -soil%sucs_vec(i,k) * &
                               (2.0*soil%bch_vec(i,k)/&
                               ((1.0+2.0*soil%bch_vec(i,k))))**(-soil%bch_vec(i,k))
      end do
   end do
   do i=1,mp
      soil%wbc_GW(i) = 2.0*soil%GWbch_vec(i)*soil%GWssat_vec(i)/&
                                 (1.0+2.0*soil%GWbch_vec(i))
      soil%smpc_GW(i) = -soil%GWsucs_vec(i) * &
                               (2.0*soil%GWbch_vec(i)/&
                               ((1.0+2.0*soil%GWbch_vec(i))))**(-soil%GWbch_vec(i))
   end do
  ! ___________________________________________________

    do k=1,ms
       do i=1,mp
          ! _____ MMY it doesn't match to icef in SUBROUTINE ovrlndflx ______
          !ssnow%icefrac(i,k) = ssnow%wbice(i,k)/(max(ssnow%wb(i,k),0.01_r_2)) ! MMY
          ! ________________ MMY to unify the calculation ____________________
          ssnow%icefrac(i,k) = max(0._r_2, min(1._r_2, &
                               ssnow%wmice(i,k) / max(ssnow%wmtot(i,k), 0.01_r_2)) )
          ! __________________________________________________________________
          ssnow%fracice(i,k) = (exp(-gw_params%IceAlpha*(1._r_2-ssnow%icefrac(i,k)))&
                               -exp(-gw_params%IceAlpha))/(1._r_2-exp(-gw_params%IceAlpha))
       end do
    end do

    ssnow%fracice(:,:) = MAX( MIN( ssnow%fracice, 1._r_2), 0._r_2)

    IF (gw_params%ssgw_ice_switch) THEN
       wb_temp(:,1:ms) =  ssnow%wbliq(:,:)
       wb_temp(:,ms+1) = ssnow%GWwb(:)
       DO k=1,ms
          kk = MIN(k+1,ms)
          DO i=1,mp
             IF (soil%isoilm(i) .EQ. 9) THEN
!$                hk_ice_factor(i,k) = 10.0**(-gw_params%ice_impedence)                   ! replaced as per MMY -- rk4417 ! MMY@23Apr2023 I don't know why the sign is changed, need to test its impact ...
                !hk_ice_factor(i,k) = (1.0-soil%ssat_vec(i,k))**(gw_params%ice_impedence)
                hk_ice_factor(i,k) = 10.0**(gw_params%ice_impedence)

             ELSE
!$                hk_ice_factor(i,k) = 10.0**(-gw_params%ice_impedence* &                 ! replaced as per MMY -- rk4417 ! MMY@23Apr2023 keep the commented lines as a reminder for future check
!$                     ( 0.5*(ssnow%wbice(i,k)/MAX(1.0e-8,ssnow%wb(i,k)) + &
!$                     ssnow%wbice(i,kk)/MAX(1.0e-8,ssnow%wb(i,kk))) ) &
!$                     )
                !hk_ice_factor(i,k) = sqrt((1.0-ssnow%wbice(i,k))**(gw_params%ice_impedence) *&
                !                     (1.0-ssnow%wbice(i,kk))**(gw_params%ice_impedence))
                hk_ice_factor(i,k) = min(10.0**(gw_params%ice_impedence*ssnow%wbice(i,k)/(soil%ssat_vec(i,k)-soil%watr(i,k))) ,& ! MMY@23Apr2023 I don't know why the eq is changed, need to test its impact ...
                                          10.0**(gw_params%ice_impedence*ssnow%wbice(i,kk)/(soil%ssat_vec(i,kk)-soil%watr(i,kk))))
             END IF
          END DO
       END DO
       hk_ice_factor(:,ms+1) = hk_ice_factor(:,ms)
    ELSE
       wb_temp(:,1:ms) = ssnow%wb(:,:)
       wb_temp(:,ms+1) = ssnow%GWwb(:)
       DO k=1,ms
          kk = MIN(k+1,ms)
          DO i=1,mp
             hk_ice_factor(i,k) = (1.0 - 0.5_r_2*(ssnow%fracice(i,k)+ssnow%fracice(i,kk)))
          END DO
       END DO
       hk_ice_factor(:,ms+1) = hk_ice_factor(:,ms)
    END IF

    liq_ratio(:) = 1.0
    k = ms
    DO i=1,mp
       liq_ratio(i) =MIN(1.,MAX(0.,wb_temp(i,k)/MAX(ssnow%wb(i,k),1e-6) ) )
    END DO

    !aquifer ice ! MMY should be aquifer liq        
    wb_temp(:,ms+1) = liq_ratio(:) * wb_temp(:,ms+1)
    ! MMY for ssgw_ice_switch true, wb_temp(:,ms+1) = ssnow%GWwb(:) * wb_temp(:,ms)/max(ssnow%wb(:,ms))
    ! MMY for ssgw_ice_switch false, wb_temp(:,ms+1) = ssnow%GWwb(:)

    !soil potential (head) calculations can use brooks-corey
    !or hutson-cass SWC

    !call swc_smp_dsmpdw(soil,ssnow)
    if (gw_params%BC_hysteresis) then
       call brook_corey_hysteresis_swc_smp(soil,ssnow)
    elseif (gw_params%HC_SWC) then
       call hutson_cass_swc_smp(soil,ssnow)
    else
       call brook_corey_swc_smp(soil,ssnow)
    end if

    !hydraulic conductivity
    !Interfacial so uses layer i and i+1
  do k=1,ms-1                           
       ! kk=min(ms+1,k+1) ! MMY
       kk= k+1 ! MMY
       do i=1,mp
           ! ____________________ MMY ______________________
           s1(i) = 0.5_r_2*((wb_temp(i,k)-soil%watr(i,k)) + &
                            (wb_temp(i,kk)-soil%watr(i,kk))) / &
                          (0.5_r_2*((soil%ssat_vec(i,k)-soil%watr(i,k)) + &
                          (soil%ssat_vec(i,kk)-soil%watr(i,kk))))
           s1(i) = min(max(s1(i),0.01_r_2),1._r_2)
           s2(i) = soil%hyds_vec(i,k)*s1(i)**(2._r_2*soil%bch_vec(i,k)+2._r_2)
          ! _________________________________________________
          ssnow%hk(i,k)    =  s1(i)*s2(i)*hk_ice_factor(i,k)
          ssnow%dhkdw(i,k) = (2._r_2*soil%bch_vec(i,k)+3._r_2)*s2(i)*&
                            0.5_r_2/(soil%ssat_vec(i,k)-soil%watr(i,k))*&
                            hk_ice_factor(i,k)
          ! MMY Note that the dhkdw equation doesn't exactly follow the finite difference
       end do
    end do

    
    !conductivity between soil column and aquifer
    k = ms
    kk = ms+1
    DO i=1,mp

       s1(i) = 0.5_r_2*(MAX(wb_temp(i,k )-soil%watr(i,k),0.) + &
            MAX(wb_temp(i,kk)-soil%GWwatr(i),0.)) / &
            (0.5_r_2*(soil%ssat_vec(i,k)-soil%watr(i,k) +&
            soil%GWssat_vec(i)-soil%GWwatr(i)))

       s1(i) = MIN(MAX(s1(i),0.01_r_2),1._r_2)
       s2(i) = soil%hyds_vec(i,k)*s1(i)**(2._r_2*soil%bch_vec(i,k)+2._r_2)

       ssnow%hk(i,k)    = s1(i)*s2(i)*hk_ice_factor(i,k)
       ssnow%dhkdw(i,k) = (2._r_2*soil%bch_vec(i,k)+3._r_2)*&
            hk_ice_factor(i,k)*&
            s2(i)*0.5_r_2/(soil%ssat_vec(i,k)-soil%watr(i,k))
       !Aquifer                                             

       s2(i) = soil%GWhyds_vec(i)*s1(i)**(2._r_2*soil%GWbch_vec(i)+2._r_2)
       ssnow%GWhk(i)     =s1(i)*s2(i) * hk_ice_factor(i,ms+1)
       ssnow%GWdhkdw(i)  =  (2._r_2*soil%GWbch_vec(i)+3._r_2)*&
            s2(i)*0.5_r_2/(soil%GWssat_vec(i)-soil%GWwatr(i)) *&
            hk_ice_factor(i,ms+1)
    END DO


  END SUBROUTINE calc_soil_hydraulic_props

  SUBROUTINE aquifer_recharge(dt,ssnow,soil,veg) 
    !* Calculate the water movement between the bottom soil layer and 
    !  groundwater aquifer, following the equation (21) in 
    !  [Zeng and Decker 2009](https://doi.org/10.1175/2008JHM1011.1) 

    USE cable_common_module

    IMPLICIT NONE
    REAL, INTENT(in) :: dt
    TYPE (soil_snow_type), INTENT(INOUT)      :: ssnow ! soil and snow variables
    TYPE (soil_parameter_type), INTENT(INOUT)    :: soil  ! soil parameters
    TYPE (veg_parameter_type), INTENT(INOUT)     :: veg
    REAL(r_2), dimension(mp)   :: zaq
    REAL(r_2), dimension(mp,ms) :: zmm,csum_dzmm
    integer :: i,k

  !> Doing the recharge outside of the soln of Richards Equation makes it easier to track total recharge amount.
    
    csum_dzmm(:,1) = m2mm*soil%zse_vec(:,1) 
    do k=2,ms
       csum_dzmm(:,k) = csum_dzmm(:,k-1) + m2mm*soil%zse_vec(:,k)
    end do

    !Doing the recharge outside of the soln of Richards Equation makes it easier to track total recharge amount.
    !Add to ssnow at some point

    select case (gw_params%aquifer_recharge_function)     
   !* if water table depth is above the bottom soil coloumn, land cover type is lake
   !  or soil type is permanent ice, there is no water movement between bottom soil
   !  layer and groundwater aquifer. Otherwise, the water movement will be calcuated
   !  as the equation (21) in [Zeng and Decker 2009](https://doi.org/10.1175/2008JHM1011.1)
       case(0)
          ssnow%Qrecharge(:) = 0._r_2
       case(1)
          do i=1,mp
             if ((ssnow%wtd(i) .le. csum_dzmm(i,ms-1)) .or. &
                 (veg%iveg(i) .ge. 16) .or. &
                 (soil%isoilm(i) .eq. 9))  then

                ssnow%Qrecharge(i) = 0._r_2
             else
                ssnow%Qrecharge(i) = -0.5*(ssnow%hk(i,ms)+ssnow%GWhk(i))*&
                                     ((-ssnow%smp(i,ms)) -&
                                      (-ssnow%zq(i,ms))) / &
                                      (ssnow%wtd(i) - &
                                       (csum_dzmm(i,ms)-0.5*soil%zse_vec(i,ms)*m2mm))
             end if
          end do

       case default
          do i=1,mp
             if ((ssnow%wtd(i) .le. csum_dzmm(i,ms)) .or. &
                 (veg%iveg(i) .ge. 16) .or. &
                 (soil%isoilm(i) .eq. 9))  then

                ssnow%Qrecharge(i) = 0._r_2
             else
                ssnow%Qrecharge(i) = -(ssnow%hk(i,ms)+ssnow%GWhk(i))*&
                                     ((ssnow%GWsmp(i)-ssnow%smp(i,ms)) -&
                                      (ssnow%GWzq(i)-ssnow%zq(i,ms))) / &
                                     (m2mm*(soil%GWdz(i)+soil%zse_vec(i,ms)))
             end if
          end do
       end select

  END SUBROUTINE aquifer_recharge

  SUBROUTINE subsurface_drainage(ssnow,soil,veg)   
 !* Calculate horizontal drainage based on the equation (18) in
 !  [Decker, 2015](http://doi.wiley.com/10.1002/2015MS000507)

    USE cable_common_module
    USE grid_constants_mod_cbl, ONLY : lakes_cable

    IMPLICIT NONE

    TYPE (soil_snow_type), INTENT(INOUT)      :: ssnow ! soil and snow variables
    TYPE (soil_parameter_type), INTENT(INOUT)    :: soil  ! soil parameters
    TYPE (veg_parameter_type), INTENT(INOUT)     :: veg
    REAL(r_2), DIMENSION(mp)                  :: sm_tot,&!sum var
                                                 ice_factor_tot  !avg ice factor
    REAL(r_2), dimension(mp,ms)               :: dzmm, ice_factor  !ice limitation on subsurface drainage
    INTEGER, DIMENSION(mp)                    :: k_drain
    INTEGER :: i,k

    REAL(r_2), DIMENSION(17) :: Efold_mod

    Efold_mod(:) = 1.0
    !Efold_mod(1:4) = (/0.2,0.2,0.2,0.2/)
    !Efold_mod(9) = 0.25

    dzmm(:,:) = m2mm*soil%zse_vec(:,:) 

    do i=1,mp

       !Note: future revision will have interaction with river here. nned to
       !work on router and add river type cells

       !* Calulate the total subsurface runoff by following the equation (18) in
       !  [Decker, 2015](http://doi.wiley.com/10.1002/2015MS000507)
       ssnow%qhz(i)  = soil%slope(i)*soil%qhz_max(i)*&
            exp( -mm2m*ssnow%wtd(i)/ soil%qhz_efold(i) ) 
       ! MMY@23Apr2023, defined in derived_parameters:
       !   soil%qhz_max(:) = real(gw_params%MaxHorzDrainRate,r_2)
       !   soil%qhz_efold(:) = real(gw_params%EfoldHorzDrainScale,r_2)*soil%drain_dens(:)+real(gw_params%EfoldHorzDrainRate,r_2)
       ! Note that, this equation is different to the one in the trunk, I have no idea about why it is changed

      !* calculate how many layers are below water table depth and can generate subsurface runoff
       !drain from sat layers
       k_drain(i) = ms+1
       do k=ms,2,-1
          if (ssnow%wtd(i) .le. sum(dzmm(i,1:k),dim=1)) then
             k_drain(i) = k + 1
          end if
       end do
       k_drain(i) = max(k_drain(i),3)

   end do

   if (gw_params%ssgw_ice_switch) then
      do k=1,ms
         do i=1,mp
            ice_factor(i,k) = (1.0-ssnow%wbice(i,k))**gw_params%ice_impedence
            ! MMY@23Apr2023 in trunk: 
            !  ice_factor(i,k) = (10.0**(-gw_params%ice_impedence*ssnow%wbice(i,k)/(ssnow%wb(i,k)+1.0e-12)))
         end do
      end do
   else
      do k=1,ms
         do i=1,mp
            ice_factor(i,k) = (1._r_2-ssnow%fracice(i,k))
         end do
      end do

   end if

   ssnow%qhlev(:,:) = 0._r_2
   sm_tot(:)        = 0._r_2

   do i=1,mp

       if (gw_params%subsurface_sat_drainage) then ! MMY@23Apr2023 I think subsurface_sat_drainage should be the default setting
          ! MMY it doesn't make sense since sm_tot == 0.
          ! sm_tot(i) = sum(ssnow%qhlev(i,k_drain(i):ms+1),dim=1) ! MMY
          ! ______ MMY find this calculation from version : cable-2.2.3-pore-scale-model _______________
          sm_tot(i) = max(ssnow%GWwb(i)-soil%GWwatr(i),0._r_2)
          do k=k_drain(i),ms
            sm_tot(i) = sm_tot(i) + max(ssnow%wbliq(i,k)-soil%watr(i,k),0._r_2)!*dzmm(k)
          end do
          ! ____________________________________________________________________________________________
          if (sm_tot(i) .lt. 1.0e-8) then
             sm_tot(i) = 1._r_2
          end if
       end if
   end do


   do i=1,mp

       ssnow%qhlev(i,ms+1) = max((ssnow%GWwb(i) - soil%GWwatr(i)),0._r_2)*&
                       ice_factor(i,ms)*ssnow%qhz(i)/sm_tot(i)

       do k=k_drain(i),ms
          ssnow%qhlev(i,k) = max(ssnow%wbliq(i,k)-ssnow%watr_hys(i,k),0._r_2)*&
                                   ice_factor(i,k)*ssnow%qhz(i)/sm_tot(i)
       end do

       !incase every layer is frozen very dry
       ssnow%qhz(i) = sum(ssnow%qhlev(i,:),dim=1)

       !Keep "lakes" saturated forcing qhz = 0.  runoff only from lakes
       !overflowing

       IF (soil%isoilm(i) .EQ. 9 .OR. veg%iveg(i) .GE. lakes_cable) THEN

          ssnow%qhz(i) = 0._r_2
          ssnow%qhlev(i,:) = 0._r_2
       end if

    end do


  END SUBROUTINE subsurface_drainage

  SUBROUTINE saturated_fraction(ssnow,soil,veg)
    TYPE (soil_snow_type), INTENT(INOUT)      :: ssnow ! soil and snow variables
    TYPE (soil_parameter_type), INTENT(IN)    :: soil  ! soil parameters
    TYPE(veg_parameter_type) , INTENT(IN)    :: veg  ! veg parameters

    REAL(r_2), DIMENSION(mp) :: S
    REAL(r_2) :: slopeSTDmm
    INTEGER :: i,k

    !if !gw_model and !or_evap:
    !in cable_um_init_subrs.F90 or cable_parameters:
    !  ssat_vec(i,:) = ssat
    !  zse_vec(i,:)  = zse
    !  UM: slope_std read in
    ! offline: slope_std read in or set to const
    !  all gw_params set by default in cable_common
    ! doesn do anything but cannot hurt


    S(:) = 0._r_2
    DO k=1,gw_params%level_for_satfrac
       S(:) = S(:) + MAX(0.01,MIN(1.0, &
              (ssnow%wb(:,k)-den_rat*ssnow%wbice(:,k)-ssnow%watr_hys(:,k))/& 
               max(0.001,ssnow%ssat_hys(:,k)-soil%watr(:,k)-den_rat*ssnow%wbice(:,k)) ) )*soil%zse_vec(:,k)
    END DO
    S(:) = S(:)/SUM(soil%zse(1:gw_params%level_for_satfrac),dim=1)
    !srf frozen fraction.  should be based on topography
    DO i = 1,mp
       !Saturated fraction
       IF (gw_params%MaxSatFraction .GT. 1e-7 .AND. veg%iveg(i) .LT. 16) THEN
          slopeSTDmm = SQRT(MIN(MAX(&
               gw_params%MaxSatFraction*soil%slope_std(i),&
               1e-5),10000._r_2)) ! ensure some variability
          ssnow%satfrac(i)    = MAX(0._r_2,MIN(0.99_r_2,&
                                !note UM wants std03, and erf is not included then
               1._r_2 - my_erf( slopeSTDmm * sqrt(2.0* S(i)) ) ) ) ! MMY change / to *@24 Sep 2021
       ELSEIF (veg%iveg(i) .LT. 16) THEN
          ssnow%satfrac(i) = 0._r_2
       ELSE
          ssnow%satfrac(i) = 0.975
       END IF
    END DO


  END SUBROUTINE saturated_fraction

  SUBROUTINE pore_space_relative_humidity(ssnow,soil,veg)
    TYPE (soil_snow_type), INTENT(INOUT)      :: ssnow ! soil and snow variables
    TYPE (soil_parameter_type), INTENT(INOUT)    :: soil  ! soil parameters
    TYPE(veg_parameter_type), INTENT(INOUT)      :: veg

    REAL(r_2), DIMENSION(mp) :: unsat_wb,unsat_smp

    ! Need a matching array of ones to use in Mark's call to the intrinsic
    ! sign func below
    ! mgk, 24/07/2018
!$    REAL(r_2), DIMENSION(mp,ms) :: ones
    REAL(r_2), DIMENSION(mp,ms) :: minus_ones  ! rk4417 as per MMY
    
    INTEGER :: i

    !if gw_model = true
    !cable_um_init_subrs.F90 or cable_parameters:
    ! ssat(i) = ssat_vec(i,1)
    !if gw_model = false
    !cable_um_init_subrs.F90 or cable_parameters:
    !ssat_vec(i,:) = ssat(i)
    !so ssat_vec can be used although soilsnow uses ssat

    ! mgk, 24/07/2018 - fix to compile
    minus_ones = -1.  

    DO i=1,mp
       IF (veg%iveg(i) .LT. 16 .AND. soil%isoilm(i) .NE. 9 .AND. &
            ssnow%snowd(i) .LE. 1e-8 ) THEN

          unsat_wb(i) =  (ssnow%wb(i,1) - ssnow%ssat_hys(i,1)*&  
               MIN(0.95,MAX(0.0,ssnow%satfrac(i))))/(1.0 - MIN(0.95,MAX(0.0,ssnow%satfrac(i))))
          ! MMY ??? I'm not sure whether we should take wbice*den_rat into consideration here,
          !     ??? so I didn't modify.
          unsat_wb(i) = max(ssnow%watr_hys(i,1)+1e-2, min(ssnow%ssat_hys(i,1), unsat_wb(i) ) )
          
          !unsat_smp(i) = sign(soil%sucs_vec(i,1),-1.0) * min(1.0, &
          !                         (max(0.001, (unsat_wb(i)-soil%watr(i,1))/(soil%ssat_vec(i,1)-&
          !                         soil%watr(i,1)) ) )** (-soil%bch_vec(i,1)) )

          ! mgk, 24/07/2018 - fix to compile
          unsat_smp(i) = SIGN(soil%sucs_vec(i,1),minus_ones(i,1)) * MIN(1.0, & 
               (MAX(0.001, (unsat_wb(i)-soil%watr(i,1))/(soil%ssat_vec(i,1)-&
               soil%watr(i,1)) ) )** (-soil%bch_vec(i,1)) )

          unsat_smp(i) = MAX(-1.0e7,unsat_smp(i) )/1000._r_2 !m

          ssnow%rh_srf(i) = MAX(0.,MIN(1., &
               EXP(9.81*unsat_smp(i)/(ssnow%tgg(i,1)*461.4)) ) )

       ELSE

          ssnow%rh_srf(i) = 1.0

       END IF
    END DO


  END SUBROUTINE pore_space_relative_humidity

  SUBROUTINE sli_hydrology(dels,ssnow,soil,veg,canopy)
    REAL, INTENT(IN)                         :: dels ! integration time step (s)
    TYPE(soil_snow_type), INTENT(INOUT)      :: ssnow  ! soil+snow variables
    TYPE(soil_parameter_type), INTENT(INOUT)    :: soil ! soil parameters
    TYPE(veg_parameter_type) , INTENT(INOUT)    :: veg  ! veg parameters
    TYPE (canopy_type), INTENT(INOUT)           :: canopy

    LOGICAL, SAVE :: sli_call = .TRUE.

    ssnow%sucs_hys = soil%sucs_vec  
    ssnow%ssat_hys = soil%ssat_vec
    ssnow%watr_hys = soil%watr

    CALL iterative_wtd (ssnow, soil, veg, cable_user%test_new_gw)

    CALL calc_soil_hydraulic_props(ssnow,soil,veg)

    CALL  ovrlndflx (dels, ssnow, soil, veg,canopy,sli_call )

    CALL subsurface_drainage(ssnow,soil,veg)

    call aquifer_recharge(dels,ssnow,soil,veg)

  END SUBROUTINE sli_hydrology

! MMY@23Apr2023 Please keep SUBROUTINE set_unsed_gw_vars to see whether can call it in future codes to normalize
!               CABLE writing. Open to any suggestion.
!-----------------------------------------------------------------------------------------
!  subroutine set_unsed_gw_vars checked by rk4417. This subroutine is not called anywhere.
!-----------------------------------------------------------------------------------------
  
  SUBROUTINE set_unsed_gw_vars(ssnow,soil,canopy)
    TYPE(soil_snow_type), INTENT(INOUT)      :: ssnow  ! soil+snow variables
    TYPE(soil_parameter_type), INTENT(INOUT)    :: soil ! soil parameters
    TYPE (canopy_type), INTENT(INOUT)           :: canopy

    ssnow%qhlev = 0.
    ssnow%Qrecharge = 0.
    ssnow%fwtop = 0.
    ssnow%wtd = 0.
    ssnow%satfrac = 1.0
    ssnow%qhz = 0.
    ssnow%wbliq = ssnow%wb - ssnow%wbice
    canopy%sublayer_dz = 0.0
    ssnow%rtevap_sat = 0.0
    ssnow%rtevap_unsat = 0.0

    ssnow%GWwb = 0.9*soil%ssat


  END SUBROUTINE set_unsed_gw_vars

  REAL(r_2) FUNCTION my_erf(x)

    IMPLICIT NONE

    REAL(r_2), INTENT(in) :: x
    REAL(r_2)             :: tmp_val, tmp


    tmp = 1.0 / ( 1.0 + 0.5 * ABS(x) )

    tmp_val =  tmp * EXP(-ABS(x) * ABS(x) - 1.26551223 + tmp *     &
         ( 1.00002368 + tmp * ( 0.37409196 + tmp *          &
         ( 0.09678418 + tmp * (-0.18628806 + tmp *              &
         ( 0.27886807 + tmp * (-1.13520398 + tmp *          &
         ( 1.48851587 + tmp * (-0.82215223 + tmp * 0.17087277 )))))))))

    IF ( x.LT.0.0 ) tmp_val = 2.0 - tmp_val

    my_erf = 1.0 - tmp_val

  END FUNCTION my_erf

   subroutine brook_corey_swc_smp(soil,ssnow)
     !* Using Brook_Corey equation to calculate soil matric potential

      type(soil_parameter_type), intent(inout) :: soil
      type(soil_snow_type),      intent(inout) :: ssnow

      real(r_2), dimension(mp,ms+1) :: wb_temp
      real(r_2), dimension(mp) :: s_mid
      integer :: i,k

      if (gw_params%ssgw_ice_switch) then
         wb_temp(:,1:ms) = ssnow%wbliq(:,:)
         wb_temp(:,ms+1) = ssnow%GWwb(:)
         do i=1,mp
          if (ssnow%wb(i,ms) .gt. 1.0e-8) then
            wb_temp(i,ms+1) = ssnow%GWwb(i) * ssnow%wbliq(i,ms)/ssnow%wb(i,ms)
          end if
         end do
      else
         wb_temp(:,1:ms) = ssnow%wb(:,:)
         wb_temp(:,ms+1) = ssnow%GWwb(:)
      end if
     !potential from soil water rention function
     !defined as layer average
     do k=1,ms
        do i=1,mp
           s_mid(i) = (wb_temp(i,k)-soil%watr(i,k))/&  !+dri*ssnow%wbice(:,k)
               (soil%ssat_vec(i,k)-soil%watr(i,k))

           s_mid(i) = min(max(s_mid(i),0.01_r_2),1._r_2)

           ssnow%smp(i,k) = -soil%sucs_vec(i,k)*s_mid(i)**(-soil%bch_vec(i,k))

           ssnow%smp(i,k) = max(min(ssnow%smp(i,k),-soil%sucs_vec(i,k)),sucmin)

           ssnow%dsmpdw(i,k) = -soil%bch_vec(i,k)*ssnow%smp(i,k)/&
                             (max(s_mid(i)*(wb_temp(i,k)-soil%watr(i,k)),0.01_r_2)) !MMY
                     ! MMY BUG in (max(s_mid(i)*(soil%ssat_vec(i,k)-soil%watr(i,k)),0.01_r_2))
        end do
     end do
     ! _____________________ MMY BUG it forgot to calculate the aquifer __________________
          !Aquifer potential
          do i=1,mp
             s_mid(i) = (wb_temp(i,ms+1)-soil%GWwatr(i))/(soil%GWssat_vec(i)-soil%GWwatr(i))
             s_mid(i) = min(max(s_mid(i),0.001_r_2),1._r_2)

             ssnow%GWsmp(i)    = -soil%GWsucs_vec(i)*s_mid(i)**(-soil%GWbch_vec(i))
             ssnow%GWsmp(i)    = max(min(ssnow%GWsmp(i),-soil%GWsucs_vec(i)),sucmin)
             ssnow%GWdsmpdw(i) = -soil%GWbch_vec(i)*ssnow%GWsmp(i)/&
                                  (max(s_mid(i)*(wb_temp(i,ms+1)-soil%GWwatr(i)),0.01_r_2)) !MMY
          end do
     ! ____________________________________________________________________________________
  end subroutine brook_corey_swc_smp

  ! MMY@13Mar2023 Please keep subroutine hutson_cass_swc_smp, test this subroutine when there is no problem
  !               in the merged version.
   subroutine hutson_cass_swc_smp(soil,ssnow)

     !* Using Hutson-CASS equation to calculate soil matric potential
     
      type(soil_parameter_type), intent(inout) :: soil
      type(soil_snow_type),      intent(inout) :: ssnow

      real(r_2), dimension(mp,ms+1) :: wb_temp
      real(r_2), dimension(mp) :: s_mid
      integer :: i,k

   do k=1,ms
      do i=1,mp
         soil%wbc_vec(i,k) = 2.0*soil%bch_vec(i,k)*soil%ssat_vec(i,k)/&
                                 (1.0+2.0*soil%bch_vec(i,k))
         soil%smpc_vec(i,k) = -soil%sucs_vec(i,k) * &
                               (2.0*soil%bch_vec(i,k)/&
                               ((1.0+2.0*soil%bch_vec(i,k))))**(-soil%bch_vec(i,k))
      end do
   end do

   do i=1,mp
      soil%wbc_GW(i) = 2.0*soil%GWbch_vec(i)*soil%GWssat_vec(i)/&
                                 (1.0+2.0*soil%GWbch_vec(i))
      soil%smpc_GW(i) = -soil%GWsucs_vec(i) * &
                               (2.0*soil%GWbch_vec(i)/&
                               ((1.0+2.0*soil%GWbch_vec(i))))**(-soil%GWbch_vec(i))
   end do
    !potential from soil water rention function
    !defined as layer average
    do k=1,ms
       do i=1,mp
          s_mid(i) = (wb_temp(i,k)-soil%watr(i,k))/&  !+dri*ssnow%wbice(:,k)
              (soil%ssat_vec(i,k)-soil%watr(i,k))

          s_mid(i) = min(max(s_mid(i),0.01_r_2),0.9999999_r_2)

          if (wb_temp(i,k) .lt. soil%wbc_vec(i,k)) then
             ssnow%smp(i,k) = -soil%sucs_vec(i,k)*s_mid(i)**(-soil%bch_vec(i,k))

             ssnow%smp(i,k) = max(min(ssnow%smp(i,k),-soil%sucs_vec(i,k)),sucmin)

             ssnow%dsmpdw(i,k) = -soil%bch_vec(i,k)*ssnow%smp(i,k)/&
                               (max(s_mid(i)*(wb_temp(i,k)-soil%watr(i,k)),0.001_r_2)) ! MMY
                               ! MMY BUG in (max(s_mid(i)*(soil%ssat_vec(i,k)-soil%watr(i,k)),0.001_r_2))
          else
            !!! MMY BUG: in Hutson & Cass 1987 using 'wb/ssat' as independent factor
            !!! MMY      but here Mark uses effective saturation (wb-watr)/(ssat-watr)
            !!! MMY      in wet soil and wb/ssat in wet soil. This leads to a iscontinuity
            !!! MMY      in the water retention curve (smp-wb relationship), and causes
            !!! MMY      abs(smp) in wet soil is larger than in dry soil. I suggest to
            !!! MMY      use wb/ssat in HC_SWC
             ssnow%smp(i,k) = -soil%sucs_vec(i,k)* sqrt(1._r_2 - wb_temp(i,k)/soil%ssat_vec(i,k))/&
                               (sqrt(1._r_2-soil%wbc_vec(i,k)/soil%ssat_vec(i,k))*&
                              ( (soil%wbc_vec(i,k)/soil%ssat_vec(i,k))**soil%bch_vec(i,k) ))

             ssnow%dsmpdw(i,k) = -ssnow%smp(i,k)/(soil%ssat_vec(i,k)*&
                                (1._r_2 - wb_temp(i,k)/soil%ssat_vec(i,k)) )
          end if
       end do
    end do

    !Aquifer potential
    do i=1,mp
       s_mid(i) = (wb_temp(i,ms+1)-soil%GWwatr(i))/(soil%GWssat_vec(i)-soil%GWwatr(i))
       s_mid(i) = min(max(s_mid(i),0.01_r_2),1._r_2)
       if (wb_temp(i,ms+1) .lt. soil%wbc_GW(i)) then
          ssnow%GWsmp(i) = -soil%GWsucs_vec(i)*s_mid(i)**(-soil%GWbch_vec(i))

          ssnow%GWsmp(i) = max(min(ssnow%GWsmp(i),-soil%GWsucs_vec(i)),sucmin)

          ssnow%GWdsmpdw(i) = -soil%GWbch_vec(i)*ssnow%GWsmp(i)/&
                    (max(s_mid(i)*(soil%GWssat_vec(i)-soil%GWwatr(i)),0.001_r_2))
       else
          ssnow%GWsmp(i) = -soil%GWsucs_vec(i)* sqrt(1._r_2 - wb_temp(i,ms+1)/soil%GWssat_vec(i))/&
                            (sqrt(1._r_2-soil%wbc_GW(i)/soil%GWssat_vec(i))*&
                           ( (soil%wbc_GW(i)/soil%GWssat_vec(i))**soil%GWbch_vec(i) ))

          ssnow%GWsmp(i) = max(min(ssnow%GWsmp(i),-soil%GWsucs_vec(i)),sucmin)

          ssnow%GWdsmpdw(i) = -ssnow%GWsmp(i)/(soil%GWssat_vec(i)*&
                             (1._r_2 - wb_temp(i,ms+1)/soil%GWssat_vec(i)) )
       end if
    end do
   end subroutine hutson_cass_swc_smp

   ! MMY@13Mar2023 Please keep subroutine brook_corey_hysteresis_swc_smp temporarily.
   !               Hysteresis in water retention curve isn't commonly considered in
   !               land surface models. I doubt whether this subrountine is necessary
   !               for CABLE. But keep it at this moment, may delete it if this subroutine
   !               is actually broken.
   
   subroutine brook_corey_hysteresis_swc_smp(soil,ssnow)

     !* Using Brook_Corey equation to calculate soil matric potential
     !  but considering the hysteresis of water retention curve
     
   type(soil_parameter_type), intent(inout) :: soil
   type(soil_snow_type),      intent(inout) :: ssnow

   real(r_2), dimension(mp,ms) :: delta_wbliq
   real(r_2), dimension(mp) :: s_mid
   integer :: i,k,klev
   real(r_2), dimension(mp,ms+1) :: wb_temp
   real(r_2) :: tmp_smp

      if (gw_params%ssgw_ice_switch) then
         wb_temp(:,1:ms) = ssnow%wbliq(:,:)
         wb_temp(:,ms+1) = ssnow%GWwb(:) * ssnow%wbliq(:,ms)/soil%ssat_vec(:,ms)
      else
         wb_temp(:,1:ms) = ssnow%wb(:,:)
         wb_temp(:,ms+1) = ssnow%GWwb(:)
      end if

     !ensure sucs_hys is set
     ssnow%sucs_hys = ssnow%hys_fac * soil%sucs_vec
!    do k=1,ms
!       do i=1,mp
!          !on dry or wet?
!          IF (ssnow%hys_fac(i,k) .gt. 0.75) then
!             !on drying
!             ssnow%watr_hys(i,k) = soil%watr(i,k)
!             ssnow%hys_fac(i,k)      = 1.0  !drying
!             ssnow%sucs_hys(i,k) = ssnow%hys_fac(i,k)*soil%sucs_vec(i,k)
!             if (ssnow%smp_hys(i,k) .lt. -ssnow%sucs_hys(i,k)) then
!                tmp_smp = (min(0.99,-ssnow%smp_hys(i,k)/ssnow%sucs_hys(i,k)))**(-soil%bch_vec(i,k))
!                ssnow%ssat_hys(i,k) = (ssnow%wb_hys(i,k)  - ssnow%watr_hys(i,k)*(1.0-tmp_smp))/&
!                                       tmp_smp
!             else
!                ssnow%ssat_hys(i,k) = ssnow%wb_hys(i,k)
!             end if
!             ssnow%ssat_hys(i,k) = max(0.001+ssnow%watr_hys(i,k),ssnow%ssat_hys(i,k))
!
!          ELSE  !wetting
!             ssnow%ssat_hys(i,k) = gw_params%ssat_wet_factor*soil%ssat_vec(i,k)
!             ssnow%hys_fac(i,k)        = 0.5  !wetting
!             ssnow%sucs_hys(i,k) = ssnow%hys_fac(i,k)*soil%sucs_vec(i,k)
!
!             if (ssnow%smp_hys(i,k) .lt. -ssnow%sucs_hys(i,k) ) then
!                tmp_smp = (min(0.99,-ssnow%smp_hys(i,k)/ssnow%sucs_hys(i,k)))**(-soil%bch_vec(i,k))
!                ssnow%watr_hys(i,k) = (ssnow%wb_hys(i,k) - ssnow%ssat_hys(i,k) *tmp_smp)/&
!                                      (1._r_2 - tmp_smp)
!             else
!                ssnow%watr_hys(i,k) = soil%watr(i,k)
!             end if
!
!             ssnow%watr_hys(i,k) = max(0._r_2,min(ssnow%ssat_hys(i,k)-0.001, ssnow%watr_hys(i,k) ) )
!          END IF
!       END DO
!    END DO

    do k=1,ms
       do i=1,mp
          s_mid(i) = (wb_temp(i,k)        - ssnow%watr_hys(i,k)) / &
                     (ssnow%ssat_hys(i,k) - ssnow%watr_hys(i,k))

          s_mid(i) = min(max(s_mid(i),0.01_r_2),1._r_2)
          ! MMY ??? why 0.01_r_2 here but 0.001_r_2 in aquifer
          ssnow%smp(i,k) = -ssnow%sucs_hys(i,k)*s_mid(i)**(-soil%bch_vec(i,k))

          ssnow%smp(i,k) = max(min(ssnow%smp(i,k),-ssnow%sucs_hys(i,k)),sucmin)

          ssnow%dsmpdw(i,k) = -soil%bch_vec(i,k)*ssnow%smp(i,k)/s_mid(i)
       end do
    end do

    !Aquifer potential
    do i=1,mp
       s_mid(i) = (wb_temp(i,ms+1)-soil%GWwatr(i))/(soil%GWssat_vec(i)-soil%GWwatr(i))
       s_mid(i) = min(max(s_mid(i),0.001_r_2),1._r_2)
       ! MMY ???
       ssnow%GWsmp(i)    = -soil%GWsucs_vec(i)*s_mid(i)**(-soil%GWbch_vec(i))
       ssnow%GWsmp(i)    = max(min(ssnow%GWsmp(i),-soil%GWsucs_vec(i)),sucmin)
       ssnow%GWdsmpdw(i) = -soil%GWbch_vec(i)*ssnow%GWsmp(i)/&
                        (max(s_mid(i)*(wb_temp(i,ms+1)-soil%GWwatr(i)),0.01_r_2)) !MMY
    end do

  end subroutine brook_corey_hysteresis_swc_smp

  ! MMY@13Mar2023 Please keep subroutine swc_hyst_direction temporarily. Subroutine
  !               swc_hyst_direction should be kept or deleted as subroutine
  !               brook_corey_hysteresis_swc_smp
  
subroutine swc_hyst_direction(soil,ssnow,veg)
  !* Calculate soil water content and soil hydraulic parameters
  !  with the consideration of hysteresis effect
  
   type(soil_parameter_type), intent(inout) :: soil
   type(soil_snow_type),      intent(inout) :: ssnow
   TYPE(veg_parameter_type) , INTENT(INOUT)    :: veg  ! veg parameters

   real(r_2), dimension(mp,ms) :: delta_wbliq,psi_tmp
   integer :: i,k,klev
   real(r_2) :: tmp_smp

   delta_wbliq = ssnow%wbliq - ssnow%wbliq_old
   !soil hydraulic state/props  so smp out matches the wb out
   !call swc_smp_dsmpdw(soil,ssnow)
   
   if (gw_params%BC_hysteresis) then ! MMY??? redundant,calling swc_hyst_direction requires gw_params%BC_hysteresis,
      call brook_corey_hysteresis_swc_smp(soil,ssnow)
   elseif (gw_params%HC_SWC) then
      call hutson_cass_swc_smp(soil,ssnow)
   else
      call brook_corey_swc_smp(soil,ssnow)
   end if

     !switch drying/wetting curve
    do k=1,ms
       do i=1,mp
          if (delta_wbliq(i,k) .gt. 0.0 .and. &
              ssnow%hys_fac(i,k) .gt. 0.75.and.&
              ssnow%wb(i,k).lt.gw_params%ssat_wet_factor * soil%ssat_vec(i,k)) then  !avoid testing .eq. 1.0
           !layer was drying now it is wetting!
              ssnow%smp_hys(i,k) = ssnow%smp(i,k)
              ssnow%wb_hys(i,k)  = ssnow%wb(i,k)
              ssnow%ssat_hys(i,k)= gw_params%ssat_wet_factor * soil%ssat_vec(i,k)
              ssnow%hys_fac(i,k) = 0.5
              ssnow%sucs_hys(i,k) = ssnow%hys_fac(i,k)*soil%sucs_vec(i,k)
          elseif (delta_wbliq(i,k) .le. 0.0 .and. &
                  ssnow%hys_fac(i,k) .lt. 0.75 .and.&
                  ssnow%wb(i,k) .gt. soil%watr(i,k)) then
             !swtiched wetting to drying
             ssnow%smp_hys(i,k) = ssnow%smp(i,k)
             ssnow%wb_hys(i,k)  = ssnow%wb(i,k)
             ssnow%hys_fac(i,k) = 1.0
             ssnow%sucs_hys(i,k) = ssnow%hys_fac(i,k)*soil%sucs_vec(i,k)
          end if

          if (delta_wbliq(i,k) .gt. 0.0) then

             ssnow%ssat_hys(i,k)= gw_params%ssat_wet_factor * soil%ssat_vec(i,k)
             if (ssnow%smp_hys(i,k) .lt. -ssnow%sucs_hys(i,k) ) then

                tmp_smp = (min(0.99,-ssnow%smp_hys(i,k)/ssnow%sucs_hys(i,k)))**(-soil%bch_vec(i,k))
                ssnow%watr_hys(i,k) = (ssnow%wb_hys(i,k) - soil%ssat_vec(i,k) * tmp_smp)/&
                                     (1.0 - tmp_smp)
             else
                ssnow%watr_hys(i,k) = ssnow%wb_hys(i,k)
             end if

             ssnow%watr_hys(i,k) = max(0._r_2,min(ssnow%ssat_hys(i,k)-0.001, ssnow%watr_hys(i,k) ) )

          else !drying ! MMY add a space
             ssnow%watr_hys(i,k)= soil%watr(i,k)  !this is how we tell if drying
                                               !watr_hys .eq. watr
             if (ssnow%smp_hys(i,k) .lt. -ssnow%sucs_hys(i,k)) then
                tmp_smp = (min(0.99,-ssnow%smp_hys(i,k)/ssnow%sucs_hys(i,k)))**(-soil%bch_vec(i,k))
                ssnow%ssat_hys(i,k) = (ssnow%wb_hys(i,k)  - soil%watr(i,k)*(1.0-tmp_smp))/&
                                       tmp_smp
             else
                ssnow%ssat_hys(i,k) = ssnow%wb_hys(i,k)
             end if

             ssnow%ssat_hys(i,k) = max(ssnow%watr_hys(i,k)+0.001,ssnow%ssat_hys(i,k))
          end if

       end do
    end do

  if (cable_user%gw_model .and. gw_params%bc_hysteresis) then
      do klev=1,ms
         do i=1,mp
            if (soil%isoilm(i) .ne. 9 .and. veg%iveg(i) .le. 16) then

               psi_tmp(i,klev) = abs(psi_c(veg%iveg(i)))

               soil%swilt_vec(i,klev) = (ssnow%ssat_hys(i,klev)-ssnow%watr_hys(i,klev)) * &
                                        (psi_tmp(i,klev)/soil%sucs_vec(i,klev))&
                                         **(-1.0/soil%bch_vec(i,klev))+&
                                        ssnow%watr_hys(i,klev)
               soil%sfc_vec(i,klev) = (gw_params%sfc_vec_hk/soil%hyds_vec(i,klev))&
                                       **(1.0/(2.0*soil%bch_vec(i,klev)+3.0)) *&
                                       (ssnow%ssat_hys(i,klev)-ssnow%watr_hys(i,klev)) + ssnow%watr_hys(i,klev)
            end if
        end do
     end do

   end if

    end subroutine swc_hyst_direction

!`!taken from
!`!/g/data1/w35/mrd561/CABLE/CMIP6-GM2_dev_testing_tiles/core/biogeophys
!`! to test
!`SUBROUTINE GWsoilfreeze(dels, soil, ssnow)
!`  !NOTE: this is only included because gw_model uses parameters XXX_vec
!`  !these are r_2.  this breaks bitwise compatibility with trunk
!`  !if acceptable this routine does the same thing but with r_2 soil params
!`  ! if max_ice_frac always set to frozen_limit and tgg_tmp is always CTFRZ
!`
!`   REAL, INTENT(IN)                    :: dels ! integration time step (s)
!`   TYPE(soil_snow_type), INTENT(INOUT)      :: ssnow
!`   TYPE(soil_parameter_type), INTENT(INOUT)    :: soil
!`   REAL     , DIMENSION(mp,ms) :: tgg_old,tgg_new,tgg_tmp !tgg_old is previous point of when crosses freezing
!`   REAL(r_2), DIMENSION(mp,ms)           :: sicefreeze
!`   REAL(r_2), DIMENSION(mp,ms)           :: sicemelt
!`   REAL(r_2), DIMENSION(mp,ms)        :: wbice_delta,avail_por,delta_ice_vol
!`   REAL(r_2), DIMENSION(mp)           :: ice_mass,liq_mass,tot_mass
!`   INTEGER :: i,j,k
!`   REAL(r_2) :: func,funcderv,Aconst,Dconst,t_zero,t_one,dtmp
!`   REAL, DIMENSION(mp,ms) :: gammzz_snow
!`   REAL(r_2),DIMENSION(mp,ms) :: xx,max_ice_frac,iceF,den_css  !Decker and Zeng 20.9
!`   REAL(r_2) :: delta_wbliq,tmp_var
!`
!`   max_ice_frac(:,:) = zero
!`   delta_ice_vol(:,:) = zero
!`   tgg_old(:,:) = ssnow%otgg(:,:)
!`   tgg_new(:,:) = ssnow%tgg(:,:)
!`   tgg_tmp(:,:) = tgg_old(:,:)
!`
!`   gammzz_snow(:,:) = zero
!`   k=1
!`   do i=1,mp
!`      if (ssnow%isflag(i) .eq. zero.and. soil%isoilm(i) .ne. 9) then
!`           gammzz_snow(i,k) = real(Ccgsnow,r_2) * real(ssnow%snowd(i),r_2)
!`      end if
!`   end do
!`
!`   sicefreeze(:,:) = -1._r_2
!`   sicemelt(:,:) = -1._r_2
!`
!`   do k=1,ms
!`   do i=1,mp
!`
!`      ssnow%wmice(i,k)  = ssnow%wbice(i,k)*soil%zse_vec(i,k)*real(Cdensity_ice,r_2)
!`      ssnow%wmliq(i,k)  = ssnow%wbliq(i,k)*soil%zse_vec(i,k)*real(Cdensity_liq,r_2)
!`      ssnow%wmtot(i,k)  = ssnow%wmice(i,k) + ssnow%wmliq(i,k)
!`
!`      if  ((ssnow%tgg(i,k) .lt. CTFRZ)  .and. &
!`           (ssnow%tgg(i,k) .lt. ssnow%otgg(i,k))) then
!`
!`            ssnow%otgg(i,k) = min(ssnow%otgg(i,k),CTFRZ)
!`
!`            if (ssnow%wb(i,k) .gt. (soil%watr(i,k)+1.0e-6) ) then
!`               iceF(i,k) = max(zero,min(0.95_r_2,max(zero,ssnow%wbliq(i,k)-soil%watr(i,k))/&
!`                                 (ssnow%wb(i,k)-soil%watr(i,k))  )  )
!`            else
!`              iceF(i,k) = 0._r_2
!`            end if
!`            tgg_tmp(i,k) = iceF(i,k)*ssnow%otgg(i,k) + &
!`                           (1._r_2 - iceF(i,k))*ssnow%tgg(i,k)
!`
!`            Aconst =max(0.1,min(0.99_r_2,(ssnow%wb(i,k)-soil%watr(i,k))/(soil%ssat_vec(i,k)-soil%watr(i,k)) ) )
!`            Dconst = exp(1. - Aconst  )
!`
!`            if (tgg_tmp(i,k) .lt. CTFRZ) then
!`            max_ice_frac(i,k) = max(zero,ssnow%wb(i,k)*&
!`                                 (1._r_2 - exp(2._r_2*(tgg_tmp(i,k)-CTFRZ)*Aconst*Aconst)) / Dconst)
!`
!`            else
!`                max_ice_frac(i,k) = zero
!`            end if
!`
!`            max_ice_frac(i,k) = min(ssnow%wb(i,k)*0.9, max_ice_frac(i,k) )
!`
!`            delta_ice_vol(i,k) = max(zero, max_ice_frac(i,k) - ssnow%wbice(i,k) )
!`
!`            !check amount of water we have
!`            delta_ice_vol(i,k) = min(ssnow%wbliq(i,k)*real(Cdensity_liq/Cdensity_ice,r_2), delta_ice_vol(i,k) )
!`
!`            sicefreeze(i,k) = min(delta_ice_vol(i,k)*soil%zse_vec(i,k)*Cdensity_ice, &
!`                                     max(zero,(ssnow%otgg(i,k)-ssnow%tgg(i,k))*ssnow%gammzz(i,k)/CHLF) )
!`
!`      elseif ((ssnow%tgg(i,k) .gt. CTFRZ) .and. &
!`              (ssnow%tgg(i,k) .gt. ssnow%otgg(i,k)) .and. &
!`              ssnow%wbice(i,k) .gt. zero ) then
!`
!`            ssnow%otgg(i,k) = min(ssnow%otgg(i,k),CTFRZ)
!`
!`            tgg_tmp(i,k) = CTFRZ
!`
!`            delta_ice_vol(i,k) = ssnow%wbice(i,k)
!`
!`            sicemelt(i,k) = min(delta_ice_vol(i,k)*soil%zse_vec(i,k)*Cdensity_ice, &
!`                                     max(zero,(ssnow%tgg(i,k)-ssnow%otgg(i,k))*ssnow%gammzz(i,k)/CHLF))
!`
!`      endif
!`   end do
!`   end do
!`
!`   DO k = 1, ms
!`   DO i=1,mp
!`
!`
!`      if (sicefreeze(i,k) .gt. zero .and. ssnow%tgg(i,k).lt.CTFRZ .and. &
!`               ssnow%tgg(i,k).lt.ssnow%otgg(i,k) ) then
!`
!`
!`         ssnow%wbice(i,k) = ssnow%wbice(i,k) +&
!`                                  sicefreeze(i,k)/soil%zse_vec(i,k)/real(Cdensity_ice,r_2)
!`
!`         delta_wbliq = max(zero,min(ssnow%wbliq(i,k),delta_ice_vol(i,k)*real(Cdensity_ice/Cdensity_liq,r_2)))
!`
!`         ssnow%gammzz(i,k) = max(soil%heat_cap_lower_limit(i,k),  &
!`                              (1.0- soil%ssat_vec(i,k))*soil%css_vec(i,k)*soil%rhosoil_vec(i,k) &
!`                            + (ssnow%wbliq(i,k) - delta_wbliq) * REAL(Ccswat*Cdensity_liq,r_2)   &
!`                            + ssnow%wbice(i,k) * REAL(Ccsice*Cdensity_ice,r_2)&
!`                            )*soil%zse_vec(i,k) + gammzz_snow(i,k)
!`
!`         ssnow%tgg(i,k) = ssnow%tgg(i,k) + real(sicefreeze(i,k) )&
!`                             * CHLF / real(ssnow%gammzz(i,k) )
!`
!`         ssnow%wmice(i,k) = ssnow%wbice(i,k)*soil%zse_vec(i,k)*real(Cdensity_ice,r_2)
!`         ssnow%wmliq(i,k) = ssnow%wmtot(i,k) - ssnow%wmice(i,k)
!`
!`         ssnow%wbliq(i,k) = ssnow%wmliq(i,k) / (soil%zse_vec(i,k)*m2mm)
!`         ssnow%wb(i,k)    = ssnow%wbliq(i,k) + den_rat * ssnow%wbice(i,k)
!`
!`         elseif (sicemelt(i,k) .gt. zero .and. ssnow%tgg(i,k).gt.CTFRZ &
!`                .and. ssnow%wbice(i,k) .gt. 0._r_2) then
!`
!`         ssnow%wbice(i,k) = ssnow%wbice(i,k)  -  delta_ice_vol(i,k)
!`
!`         delta_wbliq = delta_ice_vol(i,k)*real(Cdensity_ice/Cdensity_liq,r_2)
!`
!`         ssnow%gammzz(i,k) =max(soil%heat_cap_lower_limit(i,k), &
!`                           (1.0 - soil%ssat_vec(i,k))*soil%css_vec(i,k)*soil%rhosoil_vec(i,k)&
!`              + (ssnow%wbliq(i,k)+delta_wbliq) * REAL(Ccswat*Cdensity_liq,r_2)   &
!`              + ssnow%wbice(i,k) * REAL(Ccsice*Cdensity_ice,r_2)&
!`                             )*soil%zse_vec(i,k)  + gammzz_snow(i,k)
!`
!`         ssnow%tgg(i,k) = ssnow%tgg(i,k) - real(sicemelt(i,k) )&
!`                             * CHLF / real(ssnow%gammzz(i,k) )
!`
!`         ssnow%wmice(i,k) = ssnow%wbice(i,k)*soil%zse_vec(i,k)*real(Cdensity_ice,r_2)
!`         ssnow%wmliq(i,k) = ssnow%wmtot(i,k) - ssnow%wmice(i,k)
!`
!`         ssnow%wbliq(i,k) = ssnow%wmliq(i,k) / (soil%zse_vec(i,k)*m2mm)
!`         ssnow%wb(i,k)    = ssnow%wbliq(i,k) + den_rat * ssnow%wbice(i,k)
!`
!`      END IF
!`
!`
!`   END DO
!`   END DO
!`
!`END SUBROUTINE GWsoilfreeze

END MODULE cable_gw_hydro_module
